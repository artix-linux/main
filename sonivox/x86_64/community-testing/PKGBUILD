# Maintainer: Torr <torr[at]artixlinux[dot]org>

pkgname=sonivox
pkgver=3.6.11
pkgrel=1.1
pkgdesc="Fork of the AOSP 'platform_external_sonivox' to use outside of Android"
arch=("x86_64")
url="https://github.com/pedrolcl/sonivox"
license=("Apache")
depends=("glibc")
makedepends=(
	"gcc"
	"make"
	"cmake"
	"gtest"
)
provides=("libsonivox.so")
source=("https://github.com/pedrolcl/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=("7d7f34508c5475dc6f1c13d1facea084c17a5bca73d085ee9842dae62329a473")

build() {
	cd "$pkgname-$pkgver"
	cmake \
		-B build \
		-DCMAKE_BUILD_TYPE="None" \
		-DCMAKE_INSTALL_PREFIX="/usr" \
		-Wno-dev
	cmake --build build
}

check() {
	cd "$pkgname-$pkgver"
	ctest --test-dir build --output-on-failure
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" cmake --install build
	install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" docs/*.pdf docs/*.html
	install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/JET_Authoring_Guidelines_files" \
		docs/JET_Authoring_Guidelines_files/*
	install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/JET_Creator_User_Manual_files" \
		docs/JET_Creator_User_Manual_files/*
	install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/JET_Programming_Manual_files" \
		docs/JET_Programming_Manual_files/*
}
