# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gi-docgen
pkgver=2022.1
pkgrel=1
pkgdesc="Documentation generator for GObject-based libraries"
url="https://gnome.pages.gitlab.gnome.org/gi-docgen/"
arch=(any)
license=(Apache GPL3)
depends=(python-jinja python-markdown python-markupsafe python-pygments
         python-toml python-typogrify)
makedepends=(python-setuptools git)
_commit=37b04455ff58cb2ec3f58917d0737c435344f2fb  # tags/2022.1^0
source=("git+https://gitlab.gnome.org/GNOME/gi-docgen.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set sw=2 et:
