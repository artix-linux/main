# Maintainer: Nathan Owens <ndowens@artixlinux.org>

pkgname=directx-shader-compiler
pkgdesc="A compiler for HLSL to DXIL (DirectX Intermediate Language)."
pkgver=1.7.2207+gde70ea29b
pkgrel=2
arch=('x86_64')
url="https://github.com/microsoft/${_pkgname}"
license=('custom')
depends=('ncurses')
makedepends=('git' 'cmake' 'ninja' 'python')
_commit=de70ea29bc8734624c71e0078fbd8d532d20bdea
source=("directx-shader-compiler::git+https://github.com/microsoft/DirectXShaderCompiler.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  
  echo 1.7.2207+g$(git rev-parse --short HEAD)
}

prepare() {
  cd "${pkgname}"

  git submodule update --init

  rm -rf build && mkdir build
}

build() {
  cd "${pkgname}"/build

  artix-cmake .. -G Ninja \
    -C ../cmake/caches/PredefinedParams.cmake \
    -DCMAKE_BUILD_TYPE=Debug \
    -DLLVM_ENABLE_LTO=False

  ninja
}

package() {
  cd "${pkgname}"

  install -m755 -d "${pkgdir}"/usr/bin
  install -m755 -d "${pkgdir}"/usr/lib
  install -m755 -d "${pkgdir}"/usr/include
  install -m755 -d "${pkgdir}"/usr/share/licenses/${pkgname}

  install build/bin/dxc "${pkgdir}"/usr/bin/
  cp build/lib/libdxcompiler.so* "${pkgdir}"/usr/lib/
  cp -r include/dxc "${pkgdir}"/usr/include/

  install LICENSE.TXT "${pkgdir}"/usr/share/licenses/${pkgname}/
  install ThirdPartyNotices.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
