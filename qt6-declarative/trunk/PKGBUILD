# Maintainer: artist <artist@artixlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-declarative
_qtver=6.4.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for QML and JavaScript languages'
depends=(qt6-base)
makedepends=(cmake ninja python at-spi2-core qt6-shadertools qt6-languageserver)
optdepends=('qt6-languageserver: for qmlls')
conflicts=(qt6-quickcontrols2)
provides=(qt6-quickcontrols2)
replaces=(qt6-quickcontrols2)
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        qmlls-crash.patch::https://code.qt.io/cgit/qt/qtdeclarative.git/patch/?id=fb6a62a0)
sha256sums=('3434e72fccfa0c929c326790723d05c155f5067746b1ab05cfd7a9ba632c4383'
            'b26df66dac81167760164609f8a4b7118782a613e78aec97a823a441ace561eb')

prepare() {
  patch -d $_pkgfn -p1 < qmlls-crash.patch # Fix qmlls crashes
}

build() {
  export CXXFLAGS+=" -ffat-lto-objects"
  cmake -B build -S $_pkgfn -G Ninja  -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_PUBLICBINDIR=usr/bin \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Install symlinks for user-facing tools
  cd "$pkgdir"
  mkdir usr/bin
  while read _line; do
    ln -s $_line
  done < "$srcdir"/build/user_facing_tool_links.txt

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
