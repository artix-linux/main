# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Christoph Haag <christoph.haag@collabora.com>

pkgname=openvr
pkgver=1.16.8
pkgrel=2
pkgdesc="API and runtime that allows access to VR hardware from multiple vendors"
arch=(x86_64)
url="https://github.com/ValveSoftware/openvr"
license=(custom)
depends=(jsoncpp libgl)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        openvr-fix-vsprintf_s.patch::${url}/pull/594.patch
        openvr-use-system-jsoncpp.patch::${url}/pull/1178.patch
        openvr-fix-1.6.18-include.patch::${url}/pull/1524.patch
        0001-Add-include-stdarg.h-to-strtools_public.cpp.patch)
sha256sums=('387c98c0540f66595c4594e5f3340a1095dd90e954ff14fd5d89cc849ba32d1b'
            '29715711e5b28e2ce2934ef21cb5e6cfd05eec7f9d47b3e9b674a6d72ca8623a'
            'a230a1f9fdf356766b27e7765fc2929344bd17edc3e7e67eb479cb016a3d8981'
            '93a39df6a5d41f87b766cc7ec0fc3e49d6cc97f88b84a642fc26c9fe07cdf54c'
            'ccb5672d1ec4ce75c8a15700be17b29f743245db0a015bff4ca8a416c037838b')

prepare() {
  cd ${pkgname}-${pkgver}
  rm -rf thirdparty/jsoncpp
  patch -Np1 -i ../openvr-fix-vsprintf_s.patch
  patch -Np1 -i ../openvr-use-system-jsoncpp.patch
  patch -Np1 -i ../openvr-fix-1.6.18-include.patch
  patch -Np1 -i ../0001-Add-include-stdarg.h-to-strtools_public.cpp.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED=1 \
    -DUSE_SYSTEM_JSONCPP=True
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
