# Maintainer: Nathan Owens <ndowens@artixlinux.org>

pkgname=egl-wayland
pkgver=1.1.11
pkgrel=1
epoch=2
pkgdesc="EGLStream-based Wayland external platform"
url="https://github.com/NVIDIA/egl-wayland"
arch=(x86_64)
license=(MIT)
depends=(wayland eglexternalplatform)
makedepends=(meson git wayland-protocols 'libglvnd>=1.3.4')
provides=(libnvidia-egl-wayland.so)
options=(debug)
_commit=b250906b64e8d7853320b24e8803992305120480  # tags/1.1.11
source=("git+$url#commit=$_commit"
        10_nvidia_wayland.json)
sha256sums=('SKIP'
            '5cccf1905a266e8e34d5ad4aad4be85390e60b1a0850a29dd9d64adc641de412')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  artix-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/egl/egl_external_platform.d" -m644 10_nvidia_wayland.json
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 sts=-1 et:
