# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=autopep8
pkgver=1.6.0
pkgrel=2
epoch=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="https://github.com/hhatto/autopep8"
license=('MIT')
depends=('python-pycodestyle' 'python-toml')
source=("https://github.com/hhatto/autopep8/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4e95c46ed8542de7dfdfb1d5e73e8551df2fce8f5371e34580359285441288abdaefde1edc4f2c9701c2883fd189a8448f761b3db0a23ea425db71f8af0ded53')

build() {
  cd autopep8-$pkgver
  python setup.py build
}

check() {
  cd autopep8-$pkgver
  python setup.py test
}

package() {
  cd autopep8-$pkgver
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
