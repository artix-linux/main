# Maintainer: Nathan <ndowens@artixlinux.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=gsoap
pkgver=2.8.123
pkgrel=2.1
pkgdesc="Offers an XML language binding to ease the development of SOAP/XML Web services in C and C/C++"
url="https://www.cs.fsu.edu/~engelen/soap.html"
arch=('x86_64')
license=('GPL' 'custom')
depends=('openssl' 'zlib' 'gcc-libs')
makedepends=('autoconf' 'automake')
options=('staticlibs')
source=("https://prdownloads.sourceforge.net/gsoap2/${pkgname}_${pkgver}.zip")
sha256sums=('e018500ac942bb7627612cc9a8229610efe293a450359c413da1a006eb7c193d')

prepare() {
  cd "$srcdir"/gsoap-2.8
#  sed -i 's/#  if !defined(_GNU_SOURCE) ||/#  if !defined(_GNU_SOURCE) \&\&/' gsoap/stdsoap2.{c,cpp}
}

build() {
  cd "$srcdir"/gsoap-2.8
  # needed for programs which use gsoap and have -fPIC enabled
  # https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=353724
  export CFLAGS="$CFLAGS -fPIC -ffat-lto-objects"
  export CXXFLAGS="$CXXFLAGS -fPIC -ffat-lto-objects"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/gsoap-2.8
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/share/gsoap/import
  mkdir -p "$pkgdir"/usr/share/gsoap/WS
  cp -R gsoap/import "$pkgdir"/usr/share/gsoap/
  cp -R gsoap/WS "$pkgdir"/usr/share/gsoap/
  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
