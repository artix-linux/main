# Maintainer: qontinuum <qontinuum@artixlinux.org>
# Contributor: Adam Fontenot <adam.m.fontenot@gmail.com>

pkgname=breezy
pkgver=3.3.1
pkgrel=1
pkgdesc='A decentralized revision control system with support for Bazaar and Git file formats'
arch=(x86_64)
url=https://www.breezy-vcs.org/
license=(GPL2)
depends=(
  python-configobj
  python-fastbencode
  python-merge3
  python-patiencediff
  python-six
  python-yaml
)
makedepends=(
  cython
  git
  python-dulwich
  python-fastimport
  python-gpgme
  python-packaging
  python-paramiko
  python-setuptools
  python-setuptools-gettext
  python-setuptools-rust
)
optdepends=(
  'python-dulwich: Git support'
  'python-fastimport: Fastimport support'
  'python-gpgme: PGP support'
  'python-paramiko: access branches over SFTP'
)
provides=(bzr)
conflicts=(bzr)
replaces=(bzr)
_tag=ab1cedb426f675812998e7a05b528ea81e9201cc
source=(git+https://github.com/breezy-team/breezy.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd breezy
  git describe --tags | sed 's/brz-//; s/-/./g'
}

build() {
  cd breezy
  python setup.py build
}

package() {
  cd breezy
  python setup.py install --root="${pkgdir}" --install-data=usr/share --optimize=1 --skip-build
  ln -s brz "${pkgdir}"/usr/bin/bzr # backwards compatibility
}

# vim: ts=2 sw=2 et:
