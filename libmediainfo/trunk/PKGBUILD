# Maintainer: Nathan <ndowens@artixlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libmediainfo
pkgver=22.09
pkgrel=1

pkgdesc="shared library for mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('curl' 'glib2' 'libmms' 'libzen' 'tinyxml2' 'graphviz')

source=("https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz")

sha512sums=('4204f0ad73f5b826ec89df88607d7e491ecdad512c6bdae12f72b551827b1e4818aa2b88a46a46cb1f8fca46f3638fbe0243e252c685ca46b2d33a9acb00198c')
b2sums=('3b9fa03ae1fafd06e494ad462ab30a19141c516b6fe61867da7cf339ee470cb56a6cdab57c4e8a498e79c7e3e4b4e5a19b4302f2db2ab0172fe26fefcc0a92f6')

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms \
    --with-libtinyxml2 \
    --with-graphviz

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
