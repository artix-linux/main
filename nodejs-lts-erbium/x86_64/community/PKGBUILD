# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=nodejs-lts-erbium
pkgver=12.22.12
pkgrel=2
pkgdesc="Evented I/O for V8 javascript (LTS release: Erbium)"
arch=(x86_64)
url="https://nodejs.org/"
license=(MIT)
depends=(openssl zlib icu libuv c-ares brotli libnghttp2) # http-parser
makedepends=(python procps-ng)
optdepends=('npm: nodejs package manager')
provides=("nodejs=$pkgver")
conflicts=(nodejs)
source=("${url}"/dist/v${pkgver}/node-v${pkgver}.tar.xz
        nodejs-shared-libuv-uvwasi.patch::https://patch-diff.githubusercontent.com/raw/nodejs/node/pull/39260.patch
        python3.patch
        libnghttp1.45.1_compat.patch)
# https://nodejs.org/download/release/latest-erbium/SHASUMS256.txt.asc
sha256sums=(bc42b7f8495b9bfc7f7850dd180bb02a5bdf139cc232b8c6f02a6967e20714f2
            05692e382d71b5b92d5b86d513b9bb01750548630506ffe5b6f1920cbc5861bd
            0e862ff8f49a062630f3032be7abb65007bfa47699d6ceeb806a7ec0f2bd2589
            81f8a060b40786c02de400620da4060483d910fe7a6b68d8328f3747caf54619)
validpgpkeys=(C4F0DFFF4E8C1A8236409D08E73BC641CC11F4C8  # Myles Borins <mylesborins@google.com>
              77984A986EBC2AA786BC0F66B01FBB92821C587A  # Gibson Fahnestock <gibfahn@gmail.com>
              B9AE9905FFD7803F25714661B63B535A4C206CA9  # Evan Lucas <evanlucas@me.com>
              DD8F2338BAE7501E3DD5AC78C273792F7D83545D  # Rod Vagg <rod@vagg.org>
              B9E2F5981AA6E0CD28160D9FF13993A75599653C  # Shelley Vohr <shelley.vohr@gmail.com>
              4ED778F539E3634C779C87C6D7062848A1AB005C  # Beth Griggs <Bethany.Griggs@uk.ibm.com>
              C82FA3AE1CBEDC6BE46B9360C43CEC45C17AB93C  # Richard Lau <rlau@redhat.com> 
              74F12602B6F1C4E913FAA37AD3A89613643B6201  # Danielle Adams <adamzdanielle@gmail.com>
              108F52B48DB57BB0CC439B2997B01419BD92F80A) # Ruy Adorno <ruyadorno@hotmail.com>

prepare() {
  cd node-v${pkgver}
  patch -p1 < ../python3.patch
  patch -p1 < ../nodejs-shared-libuv-uvwasi.patch
  patch -p1 < ../libnghttp1.45.1_compat.patch
  sed -i 's|from collections import Mapping|from collections.abc import Mapping|' deps/v8/third_party/jinja2/tests.py
  sed -i 's|from collections import Mapping|from collections.abc import Mapping|' tools/inspector_protocol/jinja2/tests.py
}

build() {
  cd node-v${pkgver}

  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-cares \
    --shared-brotli \
    --shared-nghttp2
    #--shared-http-parser \

  make
}

check() {
  cd node-v${pkgver}
  make test-only
}

package() {
  cd node-v${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
