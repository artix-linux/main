# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cgl
pkgver=0.60.6
pkgrel=1
pkgdesc='COIN-OR Cut Generation Library'
arch=(x86_64)
url='https://github.com/coin-or/Cgl'
license=(EPL)
groups=(coin-or)
depends=(coin-or-clp)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Cgl/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('9e2c51ffad816ab408763d6b931e2a3060482ee4bf1983148969de96d4b2c9ce')

build() {
  cd Cgl-releases-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd Cgl-releases-$pkgver
  make test
}

package() {
  cd Cgl-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
