# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=xmlsec
pkgver=1.2.34
pkgrel=2
pkgdesc="XML Security Library is a C library based on LibXML2"
license=('custom')
arch=('x86_64')
url="https://www.aleksey.com/xmlsec/index.html"
depends=('libxslt' 'openssl' 'gnutls' 'nss' 'libltdl')
provides=('libxmlsec1.so' 'libxmlsec1-openssl.so' 'libxmlsec1-nss.so' 'libxmlsec1-gnutls.so' 'libxmlsec1-gcrypt.so')
options=('debug')
#source=(https://www.aleksey.com/xmlsec/download/${pkgname}1-${pkgver}.{tar.gz,sig}) # signature file not compatible
source=(https://www.aleksey.com/xmlsec/download/${pkgname}1-${pkgver}.tar.gz)
sha256sums=('52ced4943f35bd7d0818a38298c1528ca4ac8a54440fd71134a07d2d1370a262')
#validpgpkeys=('') #

build() {
  cd ${pkgname}1-${pkgver}
  ./configure --prefix=/usr --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}1-$pkgver
  make -k check
}

package() {
  cd ${pkgname}1-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
