# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: mrdotx <klassiker@gmx.de>

pkgname=libptytty
pkgver=2.0
pkgrel=4
pkgdesc='An offspring of rxvt-unicode that handles pty/tty/utmp/wtmp/lastlog handling in mostly OS-independent ways'
arch=(x86_64)
url='http://software.schmorp.de/pkg/libptytty.html'
license=(GPL)
source=(http://dist.schmorp.de/$pkgname/$pkgname-$pkgver.tar.gz)
depends=(gcc-libs)
makedepends=(cmake)
sha256sums=('8033ed3aadf28759660d4f11f2d7b030acf2a6890cb0f7926fb0cfa6739d31f7')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
