# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=zita-alsa-pcmi
pkgver=0.4.0
pkgrel=1
pkgdesc="The successor of clalsadrv, provides easy access to ALSA PCM devices."
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=(GPL3)
depends=(gcc-libs glibc)
makedepends=(alsa-lib)
provides=(libzita-alsa-pcmi.so)
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2")
sha512sums=('e06ae18e657d75b354c7269fcf1a4ae56241e3efbb182c7a64611855a49e086028d4f9bd91a6bbe8da2a64357ab7190d6e0dc5ae38dc9c3457fc8ce256930a50')
b2sums=('16e4adb9e531e70528b6128997a2b899036e21bd00aed24ad257c1247c08b59f6a33c30b1d3ae22c29893fadb31af84fc3b336b405dc50c39b9ef6c9b2a90beb')

prepare() {
  cd "$pkgname-$pkgver"
  # remove optimization
  # call ldconfig in local context
  sed -e '/native/d' \
      -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
      -i source/Makefile
  # fix use of BINDIR without preceeding DESTDIR
  sed -e 's/\ $(BINDIR)/\ $(DESTDIR)$(BINDIR)/' \
      -i apps/Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make -C source
  # create lib link for building apps
  ln -sv lib$pkgname.so.$pkgver source/lib$pkgname.so
  CXXFLAGS+=" -I../source" \
  LDFLAGS+=" -L../source" \
  make -C apps
  rm -v source/lib$pkgname.so
}

package() {
  depends+=(libasound.so)

  cd "$pkgname-$pkgver"
  # libs
  make -C source DESTDIR="$pkgdir/" PREFIX=/usr SUFFIX="" install
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
  # apps
  make -C apps DESTDIR="$pkgdir/" PREFIX=/usr install
}
# vim:set ts=2 sw=2 et:
