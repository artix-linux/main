# Maintainer: Nathan <ndowens@artixlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.4.2
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64)
license=(LGPL2.1)
depends=(
  glib2
  libjson-glib-1.0.so
  libusb-1.0.so
)
makedepends=(
  gi-docgen
  git
  gobject-introspection
  gtk-doc
  meson
  umockdev
  vala
)
_tag=dd5a500ed97be6d9ed2d033b8147b7878b86005a
source=(git+https://github.com/hughsie/libgusb.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd libgusb
  git describe --tags
}

build() {
  artix-meson libgusb build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
