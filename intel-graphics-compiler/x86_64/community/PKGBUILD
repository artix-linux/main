# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.11279
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses spirv-tools zlib)
makedepends=(git cmake clang lld llvm python libunwind spirv-headers vc-intrinsics)
# https://github.com/intel/intel-graphics-compiler/issues/237
options=(!emptydirs !lto)
source=(${url}/archive/igc-${pkgver}.tar.gz
        ${pkgname}-fix-debug-spirv-llvm.patch::${url}/commit/991fd3d661efcb4fb6df63b76310d6e634b05c39.patch)
sha256sums=('175beaf2b8a39049d454c49c6f418791f294dabf643203486290da8e9b311545'
            '25f860c8161c7d39707280fd995463630bd53becbeb27c47d603785aa13ef704')

prepare() {
  patch -d ${pkgname}-igc-${pkgver} -p1 < ${pkgname}-fix-debug-spirv-llvm.patch
}

build() {
  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='13.0.1' \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__USE_PREINSTALLED_SPRIV_HEADERS=ON \
    -DIGC_OPTION__SPIRV_TOOLS_MODE=Prebuilds \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DIGC_OPTION__VC_INTRINSICS_MODE=Prebuilds \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
