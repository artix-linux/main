# Maintainer: artist <artist@artixlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-languageserver
_qtver=6.4.1
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='An implementation of the Language Server Protocol'
depends=(qt6-base)
makedepends=(cmake ninja)
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz)
sha256sums=('e355cd879cffd02719d20ca89ea97c761d2653efccaf32b02db13d9b32695dc8')

build() {
  cmake -B build -S $_pkgfn -G Ninja  -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
