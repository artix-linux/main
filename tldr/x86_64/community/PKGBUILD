# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=tldr
pkgver=3.1.0
pkgrel=1
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-shtab' 'python-colorama' 'python-termcolor')
makedepends=('python-setuptools' 'python-sphinx-argparse')
checkdepends=('python-pytest')
source=("https://github.com/tldr-pages/tldr-python-client/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('53796b789e325dcab7f49df2b304fcc2e4163254927c1ade29914f33fe41f54f4fed36d89fce5d6d1eba5266f2e709e12da6602aeac32e45b5c527906a10cd99')

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
  make -C docs
}

check() {
  cd tldr-python-client-$pkgver
  python -m pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
