# Maintainer: nous <nous@artixlinux.org>

pkgname=archivetools
pkgver=3
pkgrel=3
pkgdesc='Artix Linux Archive Tools'
arch=('any')
url='https://gitlab.archlinux.org/archlinux/archivetools'
license=('GPL2')
depends=('rsync' 'hardlink' 'xz' 'util-linux' 'bash')
makedepends=('git')
backup=('etc/archive.conf')
source=("git+https://gitlab.archlinux.org/archlinux/archivetools.git#tag=v$pkgver"
        artix.patch
)
md5sums=('SKIP'
         '52432d6cde34225b63644319f99741a1')

prepare() {
  cd $pkgname
  pwd
  # archive.sh: Exclude debug packages during repo sync
  git format-patch -1 --stdout 35e4faa80cb6 | patch -Np1
  patch -p1 < ../artix.patch
}

package() {
  cd $pkgname
  install -Dm644 archive.conf "$pkgdir/etc/archive.conf"
  install -Dm755 archive.sh "$pkgdir/usr/bin/archive"
  install -Dm755 archive-cleaner -t "$pkgdir/usr/bin"
  # system stuff
  install -Dm644 archive.sysusers "$pkgdir/usr/lib/sysusers.d/archive.conf"
  install -Dm644 archive.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/archive.conf"
}

# vim:set ts=2 sw=2 et:
