# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Pavel Borzenkov <pavel@voidptr.ru>
# Contributor: aksr <aksr at t-com dot me>

pkgname=criu
pkgver=3.17
pkgrel=1
pkgdesc='Utilities to checkpoint and restore processes in userspace'
arch=('x86_64')
url='https://criu.org'
license=('GPL2')
depends=(
  'libbsd'
  'libnet'
  'libnl'
  'protobuf-c'
  'python-protobuf'
  'gnutls'
  'nftables'
)
makedepends=(
  'git'
  'xmlto'
  'asciidoc'
)
options=('!buildflags' '!lto')
_commit='4f8f295e57e68740699479d12c1ad251e6dd859f'
source=(
  "$pkgname::git+https://github.com/checkpoint-restore/criu#commit=$_commit"
  'fix-amdgpu-install.patch'
)
b2sums=('SKIP'
        '4d5e7ed02257757f57c0f5c965ebbf3bf0ca5db85d15ac983b861aac8024513ff6f8f9b971bc4c9c0cbd26ee2797116dc3aacc7acf7e7fd04a0eeb7015dc2e83')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  patch -p1 -i ../fix-amdgpu-install.patch
}

build() {
  cd "$pkgname"

  make
}

package() {
  cd "$pkgname"

  make \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    SBINDIR=/usr/bin \
    LIBDIR=/usr/lib \
    LIBEXECDIR=/usr/lib \
    install

  # remove empty directories
  rm -rf \
    "$pkgdir/usr/include/compel/common/asm" \
    "$pkgdir/var"
}
