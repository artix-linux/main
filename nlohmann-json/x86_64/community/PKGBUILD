# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nlohmann-json
# upstream doesn't use annotated tags
_commit=4f8fba14066156b73f1189a2b8bd568bde5284c5 # v3.10.5^{}
pkgver=3.10.5
pkgrel=1
# tests are tracked in a separate repo
# https://github.com/nlohmann/json_test_data
_test_pkgver=3.0.0
pkgdesc="JSON for Modern C++"
arch=(any)
url="https://github.com/nlohmann/json"
license=(MIT)
makedepends=(cmake git)
source=("${pkgname}::git+https://github.com/nlohmann/json#commit=${_commit}?signed"
        "json_test_data-${_test_pkgver}.tar.gz::https://github.com/nlohmann/json_test_data/archive/v${_test_pkgver}.tar.gz")
sha512sums=('SKIP'
            'd9af8419b837c592ec7519cd5772651c761078a9c43cf2a309cee55c323aee0df0c233fb58a07d5ee2e77492ac8b16398de234b387eae037a60e3c9ba5b08891')
b2sums=('SKIP'
        'f0a47b41805bf1426f612e9a82efea2a3e5b1c15740c1c531d859e60dc5daeb85209b4fe363fd8fb84e3bbf01a2578c74538ba3e769726494047979f5a4d468d')
validpgpkeys=('797167AE41C0A6D9232E48457F3CEA63AE251B69') # Niels Lohmann <mail@nlohmann.me>

prepare() {
  mkdir -vp "build-test/"
  mv -v "json_test_data-${_test_pkgver}/" "build-test/json_test_data/"
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_BUILD_TYPE=None \
        -DJSON_MultipleHeaders=ON \
        -Wno-dev \
        -B build \
        -S "$pkgname"
  make VERBOSE=1 -C build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_BUILD_TYPE=None \
        -DBUILD_TESTING=ON \
        -DJSON_BuildTests=ON \
        -DJSON_MultipleHeaders=ON \
        -Wno-dev \
        -B "build-test" \
        -S "$pkgname"
  make VERBOSE=1 -C build-test
}

check() {
  make -k test -C "build-test"
}

package() {
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 "$pkgname/"{CODE_OF_CONDUCT,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "$pkgname/"LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
