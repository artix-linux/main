# Maintainer: Torr <torr@artixlinux.org>
pkgname=sdcv
pkgver=0.5.4
pkgrel=1
pkgdesc="console version of StarDict"
arch=("x86_64")
url="https://dushistov.github.io/sdcv/"
license=("GPL2")
## File: https://github.com/Dushistov/sdcv/blob/master/NEWS
changelog=Changelog.txt
depends=(
	"gcc-libs"
	"glib2>=2.36"
	"zlib"
	"readline"
)
makedepends=(
	"gcc"
	"make"
	"cmake>=3.5"
	"pkgconf"
)
source=(
	"https://github.com/Dushistov/sdcv/archive/v0.5.4.tar.gz"
)
sha256sums=(
	"9fddec393f5dd6b208991d8225f90cb14d50fa9e7735f2414035d8a2ca065f28"
)


build() {
	cd "$pkgname-$pkgver"
	cmake -D CMAKE_INSTALL_PREFIX="/usr" \
		-D BUILD_TESTS=True .
	make
}

check() {
	cd "$pkgname-$pkgver"
	ctest --output-on-failure
}

package() {
	cd "$pkgname-$pkgver"
	mkdir locale
	make install DESTDIR="$pkgdir"
}
