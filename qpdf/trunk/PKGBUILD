# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Miguel Revilla <yo at miguelrevilla.com>
# Contributor: David Sotelo <dvsotelo at gmail.com>
# Contributor: Nuno Araujo <nuno.araujo@russo79.com>

pkgbase=qpdf
pkgname=('qpdf' 'qpdf-docs')
pkgver=10.6.3
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('x86_64')
url="https://github.com/qpdf/qpdf"
license=('custom:Artistic-2.0' 'Apache')
makedepends=('libjpeg-turbo' 'zlib' 'gnutls'
             'texlive-bin' 'texlive-core' 'python-sphinx' 'python-sphinx_rtd_theme' 'texlive-latexextra')
#optdepends=('perl:	required for fix-qdf')
#checkdepends=('perl')
# https://github.com/qpdf/qpdf/releases/download/release-qpdf-10.6.3/qpdf-10.6.3.sha256
source=(https://github.com/qpdf/qpdf/releases/download/release-${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('e8fc23b2a584ea68c963a897515d3eb3129186741dd19d13c86d31fa33493811'
            'SKIP')
validpgpkeys=('C2C96B10011FE009E6D1DF828A75D10998012C7E') # "Jay Berkenbilt <ejb@ql.org>"

# howto build the docs:
# https://github.com/qpdf/qpdf/blob/main/build-scripts/build-doc

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static \
    --enable-crypto-gnutls \
    --disable-crypto-openssl \
    --disable-implicit-crypto \
    --enable-show-failed-test-output \
    --enable-doc-maintenance
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make doc-dist DOC_DEST=doc
}

check() {
  cd ${pkgbase}-${pkgver}
  make check # passes all
}

package_qpdf() {
  depends=('libjpeg-turbo' 'zlib' 'gnutls')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}/" install

  install -Dm644 completions/bash/qpdf "${pkgdir}/usr/share/bash-completion/completions/qpdf"
  install -Dm644 completions/zsh/_qpdf "${pkgdir}/usr/share/zsh/site-functions/_qpdf"

  rm -rf "${pkgdir}"/usr/share/doc

  mkdir -m755 -p "${pkgdir}"/usr/share/licenses/${pkgbase}
  install -m644 {Artistic-2.0,LICENSE.txt} "${pkgdir}"/usr/share/licenses/${pkgbase}/
}

package_qpdf-docs() {
  pkgdesc+=" (documentation)"

  cd ${pkgbase}-${pkgver}
  mkdir -m755 -p "${pkgdir}"/usr/share/doc/qpdf
  cp -a doc/singlehtml "${pkgdir}"/usr/share/doc/qpdf/
  install -Dm644 doc/qpdf-manual.pdf "${pkgdir}"/usr/share/doc/qpdf/qpdf-manual.pdf
}
