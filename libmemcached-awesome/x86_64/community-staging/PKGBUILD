# Maintainer: Nathan <ndowens@artixlinux.org>

pkgname=libmemcached-awesome
pkgver=1.1.2
pkgrel=2
pkgdesc="C/C++ client library and tools for the memcached server"
arch=('x86_64')
url="https://awesomized.github.io/libmemcached/"
license=('BSD')
depends=('gcc-libs' 'libsasl' 'libevent')
makedepends=('cmake' 'memcached' 'python-sphinx' 'python-sphinx_rtd_theme')
provides=('libmemcached')
conflicts=('libmemcached')
replaces=('libmemcached')
source=(https://github.com/awesomized/libmemcached/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('88527fc1ce9d345511e81fbddf0f8b901136efb2179b58de1113fc644c8d89e3')

build() {
  cmake -B build -S libmemcached-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=ON \
    -DBUILD_DOCS_MANGZ=OFF \
    -DBUILD_TESTING=ON \
    -DENABLE_SASL=ON \
    -DENABLE_HASH_HSIEH=ON \
    -DENABLE_HASH_FNV64=ON \
    -DENABLE_HASH_MURMUR=ON \
    -DENABLE_MEMASLAP=ON \
    -DENABLE_OPENSSL_CRYPTO=ON
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 libmemcached-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
