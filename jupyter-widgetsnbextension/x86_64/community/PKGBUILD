# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-widgetsnbextension
pkgver=4.0.2
pkgrel=1
epoch=1
pkgdesc="Interactive HTML widgets for Jupyter notebooks"
arch=('any')
url="https://pypi.python.org/pypi/widgetsnbextension"
license=('BSD')
depends=('jupyter-notebook' 'python-ipywidgets')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://pypi.io/packages/source/w/widgetsnbextension/widgetsnbextension-$pkgver.tar.gz")
sha256sums=('07f0e8582f920b24316cef16490f1aeb498f2c875d48980540e5c5dbf0ff5e2d')

build() {
  cd widgetsnbextension-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd widgetsnbextension-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir/usr/etc" "$pkgdir/etc"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
