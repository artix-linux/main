# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-widgetsnbextension
pkgver=3.6.0
pkgrel=2
epoch=1
pkgdesc="Interactive HTML widgets for Jupyter notebooks"
arch=('any')
url="https://pypi.python.org/pypi/widgetsnbextension"
license=('BSD')
depends=('jupyter-notebook' 'python-ipywidgets')
makedepends=('python-build' 'python-installer' 'python-wheel')
source=("https://pypi.io/packages/source/w/widgetsnbextension/widgetsnbextension-$pkgver.tar.gz")
md5sums=('60c9ddbc7a04ff3c92c5570ec063f9b0')

build() {
  cd widgetsnbextension-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd widgetsnbextension-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir/usr/etc" "$pkgdir/etc"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
