# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=22.08.8
pkgrel=2
pkgdesc="D-Bus interface for user account query and manipulation"
url="https://gitlab.freedesktop.org/accountsservice/accountsservice"
arch=(x86_64)
license=(GPL3)
depends=(glib2 polkit shadow libxcrypt)
makedepends=(gtk-doc gobject-introspection git meson docbook-xsl xmlto vala)
checkdepends=(python-gobject python-dbusmock)
options=(debug)
_commit=1ef3add46983af875adfed5d29954cbfb184f688  # tags/22.08.8^0
source=("git+https://gitlab.freedesktop.org/accountsservice/accountsservice.git#commit=$_commit"
        fix-tests.diff)
sha256sums=('SKIP'
            '2da027faae5e477ed9ff57e3fc3cb8cd607073835118a05dd7b86bb7ff7c47fd')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}


prepare() {
  cd $pkgname

  # Tests are broken: https://github.com/martinpitt/python-dbusmock/issues/118
  git apply -3 ../fix-tests.diff
}

build() {
  local meson_options=(
    -D systemdsystemunitdir=no \
    -D elogind=true \
    -D admin_group=wheel
    -D docbook=true
    -D gtk_doc=true
  )

  artix-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libcrypt.so)
  provides+=(libaccountsservice.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
