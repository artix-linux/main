# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=freeradius
pkgver=3.2.0
pkgrel=2
pkgdesc='The premier open source RADIUS server'
arch=('x86_64')
url='https://freeradius.org/'
license=('GPL')
depends=('krb5' 'net-snmp' 'postgresql-libs' 'mariadb-libs' 'talloc'
         'libpcap' 'libxcrypt' 'libcrypt.so' 'smbclient')
makedepends=('curl' 'freetds' 'hiredis' 'json-c' 'libmemcached' 'perl'
             'python' 'samba' 'sqlite' 'unixodbc' 'yubico-c-client')
optdepends=('curl: for REST'
            'freetds: for Sybase and MS SQL'
            'hiredis: for redis support'
            'json-c: rlm_rest module'
            'libmemcached: for memcached'
            'perl: for Perl'
            'python: for Python'
            'sqlite: for sqlite'
            'unixodbc: for ODBC'
            'yubico-c-client: for yubicloud')
options=('!makeflags' 'debug')
validpgpkeys=('BF2384EC6938B9744B03E2A620E37C25995B4F85') # FreeRADIUS - Package Signing [...] <packages@freeradius.org>
source=("ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2"{,.sig}
        'python310.patch'
        'freeradius-sysusers.conf'
        'freeradius-tmpfiles.conf'
        )
sha256sums=('a3071cd78ffcb4706217561d822ee4c760daeb277a63f36a9f11d412c3c39e56'
            'SKIP'
            '58f415a4d05f2446d2514b98f5fd5d740d7bee400745d43a82d024a3d23ab253'
            '8ecaca94c7d0f4806b326685312dd4e543ce9c6c183d3d7ad01c1a0197bdfb94'
            'f536a9aa972e3e42a6b1a6d8ee17166eb721c7cba2e80f60473811497c7bd8bc'
            )

prepare() {
  cd "$srcdir"/freeradius-server-$pkgver
  patch -Np1 -i ../python310.patch
}

build() {
  cd "$srcdir"/freeradius-server-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib/freeradius \
    --localstatedir=/var \
    --enable-heimdal-krb5 \
    --enable-reproducible-builds \
    --with-system-libltdl \
    --with-system-libtool \
    --with-udpfromto
  make PACKAGE='archlinux'
}

package() {
  cd "$srcdir"/freeradius-server-$pkgver

  make install R="$pkgdir" PACKAGE='artixlinux'
  chmod o+r "$pkgdir"/etc/raddb/*
  mv "$pkgdir"/etc/raddb "$pkgdir"/etc/raddb.default
  rm -rf "$pkgdir"/var/run

  install -D -m0644 "$srcdir"/$pkgname-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -D -m0644 "$srcdir"/$pkgname-tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf
}
