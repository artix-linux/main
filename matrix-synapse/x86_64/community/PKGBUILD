# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=1.63.1
pkgrel=1
pkgdesc="Matrix reference homeserver"
url="https://github.com/matrix-org/synapse"
arch=('any')
license=('Apache')
depends=('libwebp' 'python-ijson' 'python-jsonschema' 'python-twisted'
         'python-pyopenssl' 'python-yaml' 'python-pyasn1' 'python-pynacl'
         'python-bcrypt' 'python-frozendict'
         'python-pillow' 'python-pysaml2'
         'python-unpaddedbase64' 'python-canonicaljson'
         'python-signedjson' 'python-pymacaroons'
         'python-service-identity' 'python-msgpack'
         'python-phonenumbers' 'python-prometheus_client'
         'python-attrs' 'python-netaddr' 'python-sortedcontainers'
         'python-treq' 'python-idna' 'python-jinja' 'python-matrix-common'
         'python-bleach' 'python-typing_extensions')
makedepends=(git python-build python-installer python-wheel python-poetry)
checkdepends=('python-pip' 'python-authlib' 'python-pyjwt' 'python-lxml' 'python-parameterized'
              'python-txredisapi' 'python-hiredis')
optdepends=('perl: sync_room_to_group.pl'
            'python-psycopg2: PostgreSQL support'
            'python-lxml: URL previewing'
            'python-psutil: metrics'
            'python-pyjwt: jwt'
            'python-txredisapi: redis'
            'python-hiredis')
source=("$pkgname::git+https://github.com/matrix-org/synapse.git#tag=v$pkgver"
        'generic_worker.yaml.example'
        'sysusers-synapse.conf'
        'tmpfiles-synapse.conf')
sha256sums=('SKIP'
            'f67334856609997eac26939d77cfc520e78e98d3755543ab730d83a0f362a35e'
            'aadfdd78fe73e6eb325ee4299b8db8b97bfa2f4e7df953aa8477f442598a7ec5'
            '65588c8c64dfb84cab831cd8d028a295d753cf7322dd63053e8488466047b45f')
backup=('etc/synapse/log_config.yaml')
install=synapse.install
validpgpkeys=('053191DFF4670330465227F7A542E4ED1B0FAC09'
              'D79D3CA0B61429A8A760525A903ECE108A39DEDD'
              '02450A9EDDFEE3E0C730B786A7E4A57880C3A4A9'
              '93B2970FB2FD8855AD6E0229CB2B33F7C23D44C6')

build() {
	cd $pkgname
	python -m build --wheel --no-isolation
}

check() {
	cd $pkgname
	pip install dist/*.whl
	PYTHONPATH="$PWD" python -m twisted.trial -j8 tests
}

package() {
	cd $pkgname
	python -m installer --destdir="$pkgdir" dist/*.whl

	install -vdm755 -o 198 -g 198 "$pkgdir"/etc/synapse
	install -vDm644 "$srcdir"/generic_worker.yaml.example "$pkgdir"/etc/synapse/workers/generic_worker.yaml.example

	install -vDm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
	install -vDm644 "$srcdir"/tmpfiles-synapse.conf "$pkgdir"/usr/lib/tmpfiles.d/synapse.conf
}
