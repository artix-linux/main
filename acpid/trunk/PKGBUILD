# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us

pkgname=acpid
pkgver=2.0.33
pkgrel=1
pkgdesc='A daemon for delivering ACPI power management events with netlink support'
arch=('x86_64')
url='https://sourceforge.net/projects/acpid2/'
license=('GPL')
depends=('bash')
optdepends=('perl: use perl based examples')
replaces=('acpid2')
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything')
source=("https://downloads.sourceforge.net/sourceforge/acpid2/$pkgname-$pkgver.tar.xz"
        'anything'
        'handler.sh')
sha256sums=('0856f71b3eb34a1b663d0a8e6363dfcbc519e63d847330498898658e2972dbe8'
            'eb5230affb9dba7653890655b94e83f377c689a39131b6b13c8202ba2382c275'
            '1c1e39b7ecc57092ba9d747f452fb36d8ae0456e2e64849c9c4a2625d1d57413')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # default config
  install -Dm644 ../anything "$pkgdir/etc/acpi/events/anything"
  install -Dm755 ../handler.sh "$pkgdir/etc/acpi/handler.sh"
}

# vim:set ts=2 sw=2 et:
