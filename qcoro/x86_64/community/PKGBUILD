# Maintainer: Nathan <ndowens@artixlinux.org>

pkgbase=qcoro
pkgname=(qcoro-qt5 qcoro-qt6)
pkgver=0.7.0
pkgrel=1
pkgdesc='C++ Coroutines for Qt'
arch=(x86_64)
url='https://github.com/danvratil/qcoro'
license=(MIT)
makedepends=(cmake qt5-websockets qt6-websockets qt5-declarative qt6-declarative)
source=(https://github.com/danvratil/qcoro/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('23ef0217926e67c8d2eb861cf91617da2f7d8d5a9ae6c62321b21448b1669210')

build() {
  cmake -B build5 -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT_VERSION=5 \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build5

  cmake -B build6 -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT_VERSION=6 \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build6
}

package_qcoro-qt5() {
  depends=(qt5-websockets qt5-declarative)
  DESTDIR="$pkgdir" cmake --install build5
  install -Dm644 $pkgbase-$pkgver/LICENSES/MIT.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_qcoro-qt6() {
  depends=(qt6-websockets qt6-declarative)
  DESTDIR="$pkgdir" cmake --install build6
  install -Dm644 $pkgbase-$pkgver/LICENSES/MIT.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
