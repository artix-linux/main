# Maintainer: Nathan <ndowens@artixlinux.org>

pkgname=libmbim
pkgver=1.28.0
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
url="https://www.freedesktop.org/wiki/Software/libmbim/"
arch=(x86_64)
license=(GPL2)
depends=(glib2 bash)
makedepends=(gtk-doc meson git help2man gobject-introspection bash-completion)
provides=(libmbim-glib.so)
options=(debug)
_commit=3c2f2571d746e37d9ee9f750999b179e580056cd  # tags/1.28.0^0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libmbim.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libmbim
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libmbim
}

build() {
  artix-meson libmbim build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
