# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libhandy
pkgname=(libhandy libhandy-docs)
pkgver=1.6.3
pkgrel=1
pkgdesc="Library full of GTK+ widgets for mobile phones"
url="https://gitlab.gnome.org/GNOME/libhandy"
arch=(x86_64)
license=(LGPL)
depends=(glib2 gtk3)
makedepends=(git gobject-introspection meson vala glade gi-docgen)
checkdepends=(xorg-server-xvfb)
options=(debug)
_commit=2885c314c9c84037b6fe55ead6a232e60ebfc9c5  # tags/1.6.3^0
source=("libhandy-${pkgver}::git+https://gitlab.gnome.org/GNOME/libhandy.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libhandy-${pkgver}
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libhandy-${pkgver}
}

build() {
  artix-meson libhandy-${pkgver} build \
    -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_libhandy() {
  provides=(libhandy-1.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libhandy-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 et:
