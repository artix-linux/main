# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxslt
pkgver=1.1.36
pkgrel=1
pkgdesc="XML stylesheet transformation library"
url="https://gitlab.gnome.org/GNOME/libxslt/-/wikis"
arch=(x86_64)
license=(custom:MIT)
depends=(
  libgcrypt
  libxml2
  xz
)
makedepends=(
  git
  python
)
checkdepends=(
  docbook-xml
)
optdepends=('python: Python bindings')
provides=(lib{,e}xslt.so)
options=(debug)
_commit=bcd17763a9f371ec5c9bdce0ffa2f2eb3ef74c43  # tags/v1.1.36^0
source=("git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=$_commit"
        0001-Make-generate-id-deterministic.patch)
sha256sums=('SKIP'
            'e83c20f9a4f0c95716410c127ad10d6cde814c25d183de0d319f5dda529b7a6e')

pkgver() {
  cd libxslt
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libxslt

  # Make xsltproc output reproducible for packages which use it to generate documentation
  git apply -3 ../0001-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libxslt
  ./configure --prefix=/usr \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libxslt
  make check
}

package() {
  cd libxslt
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 Copyright
}

# vim:set sw=2 sts=-1 et:
