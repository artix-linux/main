# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxslt
pkgver=1.1.35
pkgrel=1
pkgdesc="XML stylesheet transformation library"
url="https://gitlab.gnome.org/GNOME/libxslt/-/wikis"
arch=(x86_64)
license=(custom)
depends=(libxml2 libgcrypt)
makedepends=(git python)
checkdepends=(docbook-xml)
optdepends=('python: Python bindings')
provides=(lib{,e}xslt.so)
_commit=50af4e65da712ceb59f98a7feeeab2ddc3324ab9  # tags/v1.1.35^0
source=("git+https://gitlab.gnome.org/GNOME/libxslt.git#commit=$_commit"
        0001-Make-generate-id-deterministic.patch)
sha256sums=('SKIP'
            '0b1dc7ac703e347eace1b92f7ef48735a8b6013b8e5b9620a9ca882d8bf46a74')

pkgver() {
  cd libxslt
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libxslt

  # Make xsltproc output reproducible for packages which use it to generate documentation
  git apply -3 ../0001-Make-generate-id-deterministic.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libxslt
  ./configure --prefix=/usr --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libxslt
  make check
}

package() {
  cd libxslt
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
