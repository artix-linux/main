# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Torr <torr[at]artixlinux[dot]org>
# Contributor: Daenyth <Daenyth [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=rtorrent
pkgver=0.9.8
pkgrel=3.1
pkgdesc='Ncurses BitTorrent client based on libTorrent'
url='https://rakshasa.github.io/rtorrent/'
license=('GPL')
arch=('x86_64')
depends=(
	"libtorrent=0.13.${pkgver##*.}"
	"curl"
	"xmlrpc-c"
)
makedepends=(
	"autoconf"
	"automake"
	"libtool"
	"pkgconf"
	"ncurses"
	"gcc"
	"make"
)
source=(
	"https://github.com/rakshasa/rtorrent/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
	"https://manpages.debian.org/bullseye/rtorrent/rtorrent.1.en.gz"  # For version 0.9.8.
)
sha256sums=(
	"9edf0304bf142215d3bc85a0771446b6a72d0ad8218efbe184b41e4c9c7542af"
	"562d02cbcb371f2fb9277a68e5a4ded30c1ce6b0f1a7aa50de8dd85b7a17c949"
)

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./autogen.sh
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export CXXFLAGS+=' -fno-strict-aliasing'
	./configure \
		--prefix=/usr \
		--disable-debug \
		--with-xmlrpc-c

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D doc/rtorrent.rc "$pkgdir/usr/share/doc/rtorrent/rtorrent.rc"
	install -D --mode 644 --target-directory "$pkgdir/usr/share/man/man1" ../rtorrent.1.en.gz
}
