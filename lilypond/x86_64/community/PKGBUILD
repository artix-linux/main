# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=lilypond
pkgver=2.22.2
pkgrel=2
pkgdesc="Music engraving program, devoted to producing the highest-quality sheet music possible"
arch=(x86_64)
url="https://lilypond.org"
license=(FDL1.3 GPL3 custom:OFL)
groups=(pro-audio)
# NOTE: use guile only with 2.24.x
depends=(gcc-libs ghostscript glibc gsfonts guile1.8)
makedepends=(fontconfig fontforge freetype2 glib2 imagemagick pango
python rsync t1utils texinfo texlive-core tex-gyre-fonts
texlive-langcyrillic zip)
optdepends=(
  'python: for lilypond-book and other scripts'
  'tex-gyre-fonts: for extra fonts'
  'ttf-dejavu: for extra fonts'
)
options=(debug)
source=(https://lilypond.org/downloads/sources/v${pkgver%.*}/$pkgname-$pkgver.tar.gz)
sha512sums=('c49434a540a4a5297b6b03103ee388b384027bbf7a8aaacea0435d354b29e1fbd989d3a4ca35d5420bc0187e460248b52e58f034cbcd8f665db55ee840b423c0')
b2sums=('8296c412d70cf4d25d10b1369188e0f3cbb9183504b4c48deeda0521ffeccf7f8a9fb15d861d406db5e9a3ab0a04d297d3cfa5eec06b58836f109bf25a76a5a0')

prepare() {
  cd $pkgname-$pkgver
  autoconf --force --verbose
}

build() {
  cd $pkgname-$pkgver
  export GUILE=guile1.8
  ./configure --prefix=/usr \
              --disable-texi2html
  make
}

package() {
  depends+=(libfontconfig.so libfreetype.so libglib-2.0.so
  libgobject-2.0.so libpangoft2-1.0.so libpango-1.0.so)

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" vimdir="/usr/share/vim/vimfiles" install
  install -vDm 644 LICENSE.OFL -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,NEWS}.txt README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # delete copied fonts, they are relied upon as optdepends
  rm -fv "$pkgdir/usr/share/lilypond/$pkgver/fonts/otf/"{C059,Nimbus,texgyre}*.otf
}
