# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: pumbur

pkgbase=opus
pkgname=(opus opus-docs)
pkgver=1.3.1
pkgrel=3
pkgdesc="Totally open, royalty-free, highly versatile audio codec"
url="https://www.opus-codec.org/"
arch=(x86_64)
license=(BSD)
depends=(glibc)
makedepends=(doxygen)
source=(https://archive.mozilla.org/pub/opus/opus-$pkgver.tar.gz)
sha256sums=('65b58e1e25b2a114157014736a3d9dfeaad8d41be1c8179866f144a2fb44ff9d')

build() {
  cd opus-$pkgver
  ./configure --prefix=/usr --disable-static --enable-custom-modes
  make
}

check() {
  cd opus-$pkgver
  make check
}

package_opus() {
  provides=(libopus.so)

  cd opus-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING

  cd ..
  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_opus-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 et:
