# Maintainer:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=rpcsvc-proto
pkgver=1.4.3
pkgrel=1
pkgdesc='rpcsvc protocol definitions from glibc'
arch=(x86_64)
url='https://github.com/thkukuk/rpcsvc-proto'
license=(BSD)
depends=(glibc)
source=(https://github.com/thkukuk/rpcsvc-proto/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('44afac7626147b7c4166bdd90556172bd5d7655f5837245985db7af3cc610de00e215c8cccc4ac6b6a0dd6d0a65d062dc19dab4d1cec0ef45818a7ff709df513')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
