# Maintainer: artoo <artoo@artixlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Emmanuel 'guinness' Boudreault
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=libunistring
pkgver=1.1
pkgrel=1
pkgdesc='Library for manipulating Unicode strings and C strings'
url='https://www.gnu.org/software/libunistring/'
arch=(x86_64)
license=(GPL)
depends=(glibc)
provides=('libunistring.so')
source=(https://ftp.gnu.org/gnu/$pkgname/${pkgname}-${pkgver}.tar.xz{,.sig}
        https://ftp.gnu.org/gnu/$pkgname/${pkgname}-1.0.tar.xz{,.sig})
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871'  # Daiki Ueno <ueno@unixuser.org>
              '9001B85AF9E1B83DF1BDA942F5BE8B267C6A406D') # Bruno Haible (Open Source Development) <bruno@clisp.org>
sha256sums=('827c1eb9cb6e7c738b171745dac0888aa58c5924df2e59239318383de0729b98'
            'SKIP'
            '5bab55b49f75d77ed26b257997e919b693f29fd4a1bc22e0e6e024c246c72741'
            'SKIP')
b2sums=('721adc90884006480055b95d0fa06cd862417aa02b467f1e14688292ad9c11f1e33520b14ed5dc2d2724c6df8713d3af1e8032014259d8355156cb72edfcb983'
        'SKIP'
        '8208fe33d4ac2f015b0efb56b0c7dd87afc4bb1c6ca4eb3ded86d7e2101d7b7f68bfd8991af4b6dd408282ec73f134ee0b884e761ff6d52e8a1e398326aec420'
        'SKIP')

prepare() {
  cd $pkgname-1.0
  # https://git.savannah.gnu.org/cgit/gnulib.git/commit/?id=cca32830b5
  sed -i '/pragma weak pthread_create/d' tests/glthread/thread.h
}

build() {
  cd $pkgname-1.0
  ./configure --prefix=/usr
  make
  cd ../$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-1.0 check
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  cp -a $pkgname-1.0/lib/.libs/libunistring.so.2* "$pkgdir"/usr/lib/
}

