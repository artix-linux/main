# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-metee
pkgname=('intel-metee' 'intel-metee-doc')
pkgver=3.1.3
pkgrel=1
pkgdesc='Access library for Intel CSME HECI interface'
arch=('x86_64')
url='https://github.com/intel/metee/'
license=('Apache')
makedepends=('cmake' 'doxygen')
source=("https://github.com/intel/metee/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('90f4fc0732f6496e27781d14f979194b7efab744e3b7200f71fbdf878d559dc0')

build() {
    cmake -B build -S "metee-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
        -Wno-dev
    make -C build
}

package_intel-metee() {
    depends=('glibc')
    
    make -C build DESTDIR="$pkgdir" install
    mv "${pkgdir}/usr/share/doc" .
}

package_intel-metee-doc() {
    pkgdesc+=' (documentation)'
    
    install -d -m755 "${pkgdir}/usr/share"
    mv doc "${pkgdir}/usr/share"
}
