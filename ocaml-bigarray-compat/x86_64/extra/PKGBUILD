# Maintainer: Konstantin Gizdov <arch at kge dot pw>
_pkgname='bigarray-compat'
pkgname="ocaml-${_pkgname}"
pkgver=1.0.0
pkgrel=2
pkgdesc="Compatibility library to use Stdlib.Bigarray when possible"
arch=(x86_64)
url="https://github.com/mirage/bigarray-compat"
license=('Apache')
makedepends=(dune)
depends=("ocaml>=4.13.1")
source=("https://github.com/mirage/${pkgname#ocaml-}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('c365fee15582aca35d7b05268cde29e54774ad7df7be56762b4aad78ca1409d4326ad3b34af0f1cc2c7b872837290a9cd9ff43b47987c03bba7bba32fe8a030f')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    dune build -p $_pkgname
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir "lib/ocaml"

    install -dm755 "${pkgdir}/usr/share/"
    mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"
}
