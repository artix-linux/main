# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.2.7
pkgrel=1
pkgdesc="Advanced Linux Sound Architecture - Utilities"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(GPL2)
depends=(glibc pciutils psmisc)
makedepends=(alsa-lib docbook-xsl fftw libsamplerate ncurses python-docutils
systemd xmlto)
optdepends=('fftw: for alsabat')
# we require /var/lib/alsa for state file
options=(debug emptydirs)
source=(https://www.alsa-project.org/files/pub/utils/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('1be8f617315193e6033653ac96a14bd1c3272cd9d8f3776cdb1357f35f5353652acd5975cfc5bd16278cd496f5bf409bb60432818a526282edad9a4c718a97c9'
            'SKIP')
b2sums=('dd8821c510a98d76e2986d59c01516013f01c632fb6311e6ffd2b10eabbdd68312537d977b1bf2ab48e0f181751ecf58ec1c1da3f8d4f7ee84d8f2623893a091'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --disable-alsaconf \
    --sbindir=/usr/bin \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(libasound.so libatopology.so libformw.so libmenuw.so libncursesw.so
  libpanelw.so libsamplerate.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # dir where to save ALSA state
  install -vdm 755 "$pkgdir/var/lib/alsa/"
}
