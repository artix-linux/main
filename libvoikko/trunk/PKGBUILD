# Maintainer: 
# Contributor:	Jesse Jaara	<gmail.com: jesse.jaara>
# Contributor:	Jussi Timperi	<gmail.com: jussi.timperi>

pkgname=libvoikko
pkgver=4.3.1
pkgrel=2
pkgdesc="A spelling and grammar checker, hyphenator and collection of related linguistic data for Finnish language"
arch=(x86_64)
url="http://voikko.sourceforge.net"
license=(GPL2)
depends=(gcc-libs)
makedepends=(python)
source=(https://www.puimula.org/voikko-sources/libvoikko/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('6677fac7c0cba1289dda23b0c0a4bbe44480ce747189d40634ac57811728c1942a795efd50270e933373fabab9c54e82a8f9fde06d936442820e3f8019574f08'
            'SKIP')
validpgpkeys=('AC5D65F10C8596D7E2DAE2633D309B604AE3942E') # Harri Pitk√§nen <hatapitk@iki.fi>

build() {
  cd $pkgname-$pkgver

# Workaround encoding error
  export LANG=en_US.UTF-8
  ./configure --prefix=/usr --enable-hfst=false --with-dictionary-path=/usr/share/voikko
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/lib/python3.10/site-packages
  cp python/libvoikko.py "$pkgdir"/usr/lib/python3.10/site-packages/libvoikko.py
}
