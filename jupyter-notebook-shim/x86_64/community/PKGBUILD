# Maintainer: Antonio Rojas <arojas@archlinux.com>

_pyname=notebook_shim
pkgname=jupyter-notebook-shim
pkgver=0.1.0
pkgrel=1
pkgdesc='Provides a way for JupyterLab and other frontends to switch to Jupyter Server for their Python Web application backend'
arch=(any)
url='https://github.com/jupyterlab/notebook_shim'
license=(BSD)
depends=(jupyter-server)
makedepends=(python-build python-installer)
checkdepends=(python-pytest-tornasync)
source=(https://pypi.python.org/packages/source/n/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('7897e47a36d92248925a2143e3596f19c60597708f7bef50d81fcd31d7263e85')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver/$_pyname
  pytest
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/{usr/,}etc
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
