# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=opencl-clang
pkgname=intel-${_srcname}
pkgver=13.0.0.r6+gd06733f
pkgrel=1
pkgdesc="Wrapper library around clang that can compile OpenCL C kernels to SPIR-V modules"
arch=(x86_64)
url="https://github.com/intel/opencl-clang"
license=(custom)
depends=(clang llvm-libs spirv-llvm-translator)
makedepends=(cmake llvm git)
# Current point in the used LLVM branch
_commit=d06733f695682761c3df30c7a36c52ea7b4b3dd8
source=("git+${url}.git#commit=$_commit"
	"https://gitea.artixlinux.org/artixlinux/packages-llvm/raw/commit/197115384456f683bee2ed76913c828546f5afae/intel-opencl-clang/trunk/0001-llvm-header-location.patch")
sha256sums=('SKIP'
            '8a16cf0c5e696400901627b06aecb368eb4cb301ef79435a1f0517f99dee0af3')

pkgver() {
  cd ${_srcname}
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./;s/-/+/'
}

prepare() {
  cd ${_srcname}
  patch -Np1 -i ../0001-llvm-header-location.patch
}

build() {
  cmake -B build -S ${_srcname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLLVMSPIRV_INCLUDED_IN_LLVM=OFF \
    -DSPIRV_TRANSLATOR_DIR=/usr \
    -DLLVM_NO_DEAD_STRIP=ON \
    -DPREFERRED_LLVM_VERSION=13.0.1 \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${_srcname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
