# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: David Rosenstrauch <darose@darose.net>

pkgname=ddclient
pkgver=3.9.1
pkgrel=1.1

pkgdesc="Update dynamic DNS entries for accounts on many dynamic DNS services"
url="https://github.com/ddclient/ddclient"
arch=('any')
license=('GPL2')

backup=('etc/ddclient/ddclient.conf')

depends=('perl-io-socket-inet6' 'perl-io-socket-ssl' 'perl-digest-sha1'
         'net-tools' 'perl-data-validate-ip')
optdepends=('smtp-forwarder: email support requires sendmail binary')

source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ddclient/ddclient/archive/v$pkgver.tar.gz
        tmpfiles
        sysusers)

sha512sums=('a8a4d6cb94e4239a7b7b4fc7d9ebef703cbd6c45fc3394b644694a053b5b8aa8d109410b5b8b3676a5f30b18474d24c7feb16c65c30b28bd7d941d8a214b1346'
            '13c6e541ff23575d799eee943b8e10e277e5073732cd7c9705c23ee557bd99978435c0fe8b546fa3122a60cfb0499e27853e1968f0c1e0225890b68ae678e0c7'
            '4a3ce3e00783c99030740f407fdbb9e6185a1ea6faff809a7ca415c1999bd55f5cafffe278b68ee4a428fb56e01ff36ac12ad4f28c4954816ff68f28a78136a8')

package() {
  cd ddclient-$pkgver

  install -Dm755 ddclient "$pkgdir"/usr/bin/$pkgname
  install -Dm600 sample-etc_ddclient.conf "$pkgdir"/etc/ddclient/ddclient.conf
  install -d "$pkgdir"/var/cache/ddclient

  install -Dm644 README.cisco "$pkgdir"/usr/share/doc/ddclient/README.cisco
  install -Dm644 README.md "$pkgdir"/usr/share/doc/ddclient/README.md
  install -Dm644 README.ssl "$pkgdir"/usr/share/doc/ddclient/README.ssl
  install -Dm644 sample-etc_cron.d_ddclient "$pkgdir"/usr/share/doc/ddclient/sample-etc_cron.d_ddclient
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/COPYRIGHT

  install -Dm644 "$srcdir"/tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/ddclient.conf
  install -Dm644 "$srcdir"/sysusers "$pkgdir"/usr/lib/sysusers.d/ddclient.conf
}
