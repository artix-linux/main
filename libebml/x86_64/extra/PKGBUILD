# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=libebml
pkgver=1.4.2
pkgrel=2
pkgdesc='Extensible Binary Meta Language library'
arch=(x86_64)
url=https://github.com/Matroska-Org/libebml
license=(LGPL2.1)
depends=(gcc-libs)
makedepends=(
  cmake
  git
  ninja
)
provides=(libebml.so)
_tag=6c59e5e1ce0087e4465f6d3f479449d3f6bcb167
source=(
  git+https://github.com/Matroska-Org/libebml.git#tag=${_tag}
  libebml-limits.patch
)
b2sums=('SKIP'
        '62aaeb70d83c91af422b70a3e21ef7e538201c01e1b088a7e05d0bde02078fa6ed723ea3d0dc01da0bfbac4012e2b0c683de7abb26eb5d38553e7905f0ff8bfd')

prepare() {
  cd libebml
  patch -Np1 -i ../libebml-limits.patch
}

pkgver() {
  cd libebml
  git describe --tags | sed 's/^release-//'
}

build() {
  cmake -S libebml -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}

# vim: ts=2 sw=2 et:
