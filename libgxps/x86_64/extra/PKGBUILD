# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgxps
pkgver=0.3.2
pkgrel=3
pkgdesc="XPS Documents library"
url="https://wiki.gnome.org/Projects/libgxps"
arch=(x86_64)
license=(GPL2)
depends=(gtk3 libarchive libjpeg-turbo libtiff lcms2)
makedepends=(gobject-introspection gtk-doc git meson)
options=(debug)
_commit=f5027ed09c767e8a02e652fbc77dcea79e19e10d  # tags/0.3.2^0
source=("git+https://gitlab.gnome.org/GNOME/libgxps.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libgxps
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libgxps
}

build() {
  artix-meson libgxps build -D enable-man=true -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
