# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-media-sdk
pkgname=('intel-media-sdk' 'libmfx')
pkgver=22.4.2
pkgrel=1
pkgdesc='Legacy API for hardware video acceleration on Intel GPUs (Broadwell to Rocket Lake)'
arch=('x86_64')
url='https://software.intel.com/en-us/media-sdk/'
license=('MIT')
makedepends=('libdrm' 'libva' 'wayland' 'intel-media-driver'
             'cmake' 'libpciaccess' 'libx11' 'libxcb' 'python')
source=("https://github.com/Intel-Media-SDK/MediaSDK/archive/intel-mediasdk-${pkgver}.tar.gz"
        '010-intel-media-sdk-fix-reproducible-build.patch'::'https://github.com/Intel-Media-SDK/MediaSDK/commit/f6925886f27a39eed2e43c5b7b6c342d00f7a970.patch'
        '020-intel-media-sdk-gcc12-fix.patch'::'https://github.com/Intel-Media-SDK/MediaSDK/commit/6c58469d03d7a50760a8594b129a78db0262a1cc.patch')
sha256sums=('b327c7eedf155a324f47d36550eb4074e97344c1d486ef296b8f2a680bd4a122'
            'f1d8a4edf953cfec1516f1a8383c5ee033245aba16cfae0bc79b7de1a6365fcc'
            '39241e2dfdc16e3452b31650752b29ad899afdf787117a8463656d9e39c9d566')

prepare() {
    patch -d "MediaSDK-intel-mediasdk-${pkgver}" -Np1 -i "${srcdir}/010-intel-media-sdk-fix-reproducible-build.patch"
    patch -d "MediaSDK-intel-mediasdk-${pkgver}" -Np1 -i "${srcdir}/020-intel-media-sdk-gcc12-fix.patch"
}

build() {
    export CFLAGS+=' -ffat-lto-objects'
    export CXXFLAGS+=' -ffat-lto-objects'
    cmake -B build -S "MediaSDK-intel-mediasdk-${pkgver}" \
        -DBUILD_ALL:BOOL='ON' \
        -DBUILD_TOOLS:BOOL='ON' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DENABLE_ITT:BOOL='OFF' \
        -DENABLE_OPENCL:BOOL='OFF' \
        -DENABLE_WAYLAND:BOOL='ON' \
        -DENABLE_X11_DRI3:BOOL='ON' \
        -DMFX_APPS_DIR='/usr/lib/mfx' \
        -Wno-dev
    make -C build
}

check() {
    make -C build test
}

package_intel-media-sdk() {
    depends=('libdrm' 'libva' 'wayland' "libmfx=${pkgver}" 'intel-media-driver')
    options=('!emptydirs')
    
    make -C build DESTDIR="$pkgdir" install
    ln -s mfx/samples/libcttmetrics.so "${pkgdir}/usr/lib/libcttmetrics.so"
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
    
    [ -d 'libmfx' ] && rm -rf libmfx
    mkdir -p libmfx/usr/{include,lib/pkgconfig}
    mv "${pkgdir}/usr/include/mfx" libmfx/usr/include
    mv "${pkgdir}/usr/lib"/libmfx.so* libmfx/usr/lib
    mv "${pkgdir}/usr/lib/pkgconfig"/{,lib}mfx.pc libmfx/usr/lib/pkgconfig
}

package_libmfx() {
    pkgdesc='Intel Media SDK dispatcher library'
    depends=('gcc-libs')
    
    mv -T libmfx "$pkgdir"
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
