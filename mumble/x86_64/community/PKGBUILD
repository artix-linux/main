# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Sebastian.Salich@gmx.de
# Contributor: Doc Angelo

pkgbase=mumble
pkgname=('mumble' 'murmur')
pkgver=1.4.287
pkgrel=5
pkgdesc="An Open Source, low-latency, high quality voice chat software"
arch=('x86_64')
url="https://www.mumble.info/"
license=('BSD')
# shared depends
depends=('gcc-libs' 'glibc' 'openssl' 'qt5-base')
# shared makedepends
makedepends=('avahi' 'boost' 'cmake' 'poco' 'protobuf' 'python' 'qt5-tools' 'speech-dispatcher')
# mumble makedepends
makedepends+=('alsa-lib' 'hicolor-icon-theme' 'jack' 'libpulse' 'libsndfile'
'libspeechd' 'libx11' 'libxi' 'mesa' 'opus' 'qt5-svg' 'speex' 'xdg-utils' 'lib32-gcc-libs')
# murmur makedepends
makedepends+=('grpc' 'libcap' 'zeroc-ice')
source=(
  "https://github.com/mumble-voip/mumble/releases/download/v${pkgver}/mumble-${pkgver}.tar.gz"{,.sig}
  "murmur.dbus.conf"
  "murmur.sysusers"
  "murmur.tmpfiles"
  "${pkgname}-1.4.230-configuration.patch"
    ${pkgname}-1.4-openssl3.patch
    ${pkgname}-1.4-crypto-threads.patch
    ${pkgname}-1.4-odr.patch
)
sha512sums=('34ed30c18257ba8deae6938009a90147c8bc3a0aca28e69bea7ec0262e8d2cdacb9a840fac7d3dd623a52ef8d5903ed5424b62b483af21d6df6aa9632eae9d82'
            'SKIP'
            '97c7effdddec324e40195c36ef4927950a5de26d2ee2d268d89df6fb547207bbbe30292773316cae6f57ec9923244f205fb0edc377b798771ba7385e3c11d86a'
            '5af28d0c2b2b072cfbd500b5f63549e88a86cf3fc15e4d2df89e787c4d2bafdecbe078a518e0d1b25d82f9873cb06838ec1c9ebed625ffb7e8c80fcd942ebf74'
            '411784e8e0dcf6c163780ae895ae1a6bdad0bb2dd2b128911c484ac3eff073d95c5791b625493a2b8296d24bd7e6ac72d3c42180817e48b29f0c6a8fd841807c'
            'e35dee4ce1cca15747330ba21892064adec85feb9937a89aea95ccf2c668746075fbe1c343e26ba2ac26a9e36144fb0b3eb0f44d85e6d258040bcd3500818a31'
            'e6697efce8db0a863ccd6215b531cf2843de108e907ce41b35cd351ca50e2b59b33f13fa5566f9e4f6fff713a96d84d68c0f8d3fce831acf3321fa0cae029842'
            '2d0fcb11005f52b58d5dc43ac421fcccd9204a3ead36e0d4b4e1893818bcb9bf509d1459c358c0a8d02b0ff749738a262b9fe321e049bd65d8b2a2b441603d99'
            '33ac86bd991dc197b04380fcaf49fdde83033afc66efe79aa08a3533ea187d8966e333362edffce8ff78d48544051317f727c730c7dc3db74e628ab98aeaaaf8')
b2sums=('5fc89c184aa54ab8269870fd87b6c9ce271d77c05a6ecb2aa78eccf297ffb842a50a18a142ac628c1b287a2b5e6c0ae0dced3237242303840a4de05b7f3e7040'
        'SKIP'
        'a416d071d9658fc3f3ea267d6ff24237317f5c05b59fe1578e5a4cb4911840896bf5e524a143f5c70a797da9f343312468720f03343b4b66bea20e94aa316cc2'
        '9909371a0d1c06d2aa00f161aebb19266aad0253b407dd01da5325f4f307678bba28a89596bd2ec97df5844068856c10bb4b10acad7bf170dd2853b3e11c48cd'
        '999b2848c718f25a2d8257595194521e08d36d101906d76b137218f0a19f2451b7e1adc0c013e1f1c3448145b89a1e88f46cb1c9da27777737379227d3574d10'
        'd77b81c5ac39561e3df087ad325e88909b083dad95a25c7106a941645354c87d07ea5d41eec5aa4a77a402cadbe8230a4a1c641d8414d11174e4b3254fc18647'
        '3eb80f7030c8131e61fdffbdf19522fdea8b74b6d99144e7dcc2046c8fe5d886625fab206092564e175be71e21637b7468de34b8fef547c5096be767c8952403'
        '87d9ca8879cd09b8f29d0942ae84dfe4de7ab0216f50f2ff7f8b2802017802083b38793f9703de3bc4de1a083fc17a353f5527405740c68d826e8e82092455cf'
        '7d9457e6e45b939bc2cd2c4522fc37e6bcbdbad74a06006aad085f7c8e4f7fbb30be307b59611f1ab77518411ac5aec2a938359326a539eddf5e0b2f04fbc5c1')
# See https://github.com/mumble-voip/mumble-gpg-signatures
validpgpkeys=(
'1EDEBE2A93CB97FA9903D52E25F63C66245DFC60'  # Mumble Automatic Build Infrastructure 2022 <mumble-auto-build-2022@mumble.info>
)

prepare() {
  cd "${pkgbase}-${pkgver}.src"
  # gentoo patches
  patch -Np1 -i ../"${pkgname}-1.4-openssl3.patch"
  patch -Np1 -i ../"${pkgname}-1.4-crypto-threads.patch"
  patch -Np1 -i ../"${pkgname}-1.4-odr.patch"

  # set default values in configuration file
  patch -Np1 -i ../"${pkgname}-1.4.230-configuration.patch"
}

build() {
  # upstream requires adding arbitrary build number specifically, as otherwise the version string is wrong:
  # https://github.com/mumble-voip/mumble/issues/5538
  local _build_number="$(cut -d '.' -f 3 <<< "$pkgver")"
  local _default_options=(
    -D CMAKE_INSTALL_PREFIX=/usr
    -D CMAKE_BUILD_TYPE=None
    -D BUILD_NUMBER="$_build_number"
    -D warnings-as-errors=OFF
    -W no-dev
    -S "${pkgbase}-${pkgver}.src"
  )

  artix-cmake "${_default_options[@]}" \
        -Dbundled-opus=OFF \
        -Dbundled-speex=OFF \
        -Dupdate=OFF \
        -Dserver=OFF \
        -B build-client
  make VERBOSE=1 -C build-client

  artix-cmake "${_default_options[@]}" \
        -Dgrpc=ON \
        -Dclient=OFF \
        -B build-server
  make VERBOSE=1 -C build-server
}

package_mumble() {
  pkgdesc+=" (client)"
  # NOTE: avahi, jack, libpulse, opus is dlopen'ed
  depends+=('avahi' 'hicolor-icon-theme' 'jack' 'libasound.so' 'libprotobuf.so'
  'libpulse' 'libsndfile.so' 'libspeechd' 'libx11' 'libxi' 'opus' 'poco'
  'qt5-svg' 'speex' 'xdg-utils')
  optdepends=(
    'lib32-glibc: for mumble-overlay'
    'espeak-ng: Text-to-speech support'
    'speech-dispatcher: Text-to-speech support'
  )

  make DESTDIR="${pkgdir}" install -C build-client
  install -vDm 644 "${pkgbase}-${pkgver}.src/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}

package_murmur() {
  pkgdesc+=" (server)"
  # NOTE: avahi is dlopen'ed
  depends+=('avahi' 'libcap.so' 'libdns_sd.so' 'libgrpc++.so' 'libprotobuf.so' 'zeroc-ice')
  backup=('etc/murmur.ini')
  install=murmur.install

  make DESTDIR="${pkgdir}" install -C build-server
  # renaming of the server component was not done in full capacity: https://github.com/mumble-voip/mumble/issues/5436
  mv -v "${pkgdir}/usr/bin/"{mumble-server,murmurd}
  mv -v "${pkgdir}/usr/share/man/man1/"{mumble-server,murmurd}.1
  mv -v "${pkgdir}/usr/share/man/man1/"{mumble-server,murmur}-user-wrapper.1

  (
    cd "${pkgbase}-${pkgver}.src"
    install -vDm 640 scripts/murmur.ini -t "${pkgdir}/etc"
    install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
    install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -vDm 644 src/murmur/{Murmur.ice,MurmurRPC.proto} -t "${pkgdir}/usr/share/${pkgname}"
  )

  install -vDm 644 murmur.dbus.conf "${pkgdir}/usr/share/dbus-1/system.d/murmur.conf"
  install -vDm 644 murmur.sysusers "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 murmur.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
# vim: sw=2:ts=2 et:
