# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=elogind
pkgname=('elogind' 'libelogind')
pkgver=246.10
pkgrel=10
pkgdesc="The systemd project's logind, extracted to a standalone package"
arch=('x86_64')
url="https://github.com/elogind/elogind"
license=('GPL' 'LGPL2.1')
makedepends=('acl' 'intltool' 'libtool' 'gperf' 'gtk-doc' 'polkit' 'dbus'
            'libseccomp' 'meson' 'kexec-tools' 'openrc' 'libcap')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/elogind/elogind/archive/v${pkgver}.tar.gz"
        dbus-exit.patch::https://github.com/pierre-labastie/elogind/commit/04d76bc0f087640bedc6ee52525529ccfdc6ae78.patch
        fix-p-opt.patch::https://github.com/njhanley/elogind/commit/850797eaf482d7f1ada051730eeeca53676ca43e.patch)
sha256sums=('c490dc158c8f5bca8d00ecfcc7ad5af24d1c7b9e59990a0b3b1323996221a922'
            'ece95a634a82080d2069d17760e82a3a7c59bf9ef2236068e29dfdfa4e838696'
            'c27c07101504bfb610110fc5bc26a41c549ad39e6c26643296c29b8f1bafb6d4')

prepare() {
    cd ${pkgbase}-${pkgver}
    patch -Np 1 -i ../dbus-exit.patch
    patch -Np 1 -i ../fix-p-opt.patch
}

build() {
    local meson_options=(
        -Drootlibdir=/usr/lib
        -Drootlibexecdir=/usr/lib/elogind
        -Ddbuspolicydir=/usr/share/dbus-1/system.d
        -Ddocdir=/usr/share/doc/elogind
        -Ddefault-hierarchy=hybrid
        -Dcgroup-controller=openrc
        -Ddefault-kill-user-processes=false
        -Dman=true
    )

    artix-meson "$pkgbase-${pkgver}" build "${meson_options[@]}"

    meson compile -C build
}

check(){
    meson test -C build --print-errorlogs
}

package_elogind() {
    pkgdesc="The systemd project's logind, extracted to a standalone package"
    provides=('logind')
    depends=('acl' 'dbus' 'libseccomp' 'libelogind' 'kexec-tools' 'udev')
    optdepends=('polkit: polkit support')
    backup=('etc/elogind/logind.conf')

    DESTDIR="$pkgdir" meson install -C build

    install -dm755 "${srcdir}"/_libelogind/{include,pkgconfig,man3}
    mv -v "${pkgdir}"/usr/lib/libelogind*.so* "${srcdir}"/_libelogind
    mv -v "${pkgdir}"/usr/lib/pkgconfig/libelogind.pc "${srcdir}"/_libelogind/pkgconfig
    mv -v "${pkgdir}"/usr/include/* "${srcdir}"/_libelogind/include
    mv -v "${pkgdir}"/usr/share/man/man3 "${srcdir}"/_libelogind/

    install -d "${pkgdir}"/{etc,usr/lib}/elogind/{logind,sleep}.conf.d
}

package_libelogind(){
    pkgdesc="elogind client libraries"
    provides=('libelogind.so' 'liblogind')
    depends=('libcap' 'libudev.so')

    cd "${pkgbase}-${pkgver}"

    install -dm755 "${pkgdir}"/usr/{lib/pkgconfig,include,share/man/man3}
    mv "${srcdir}"/_libelogind/pkgconfig/* "${pkgdir}"/usr/lib/pkgconfig/
    mv "${srcdir}"/_libelogind/include/* "${pkgdir}"/usr/include
    mv "${srcdir}"/_libelogind/man3 "${pkgdir}"/usr/share/man
    mv -v "${srcdir}"/_libelogind/libelogind*.so* "${pkgdir}"/usr/lib


    ln -sfv libelogind.so "${pkgdir}"/usr/lib/libsystemd.so
    ln -sfv libsystemd.so "${pkgdir}"/usr/lib/libsystemd.so.0

    ln -sfv libelogind.pc "${pkgdir}"/usr/lib/pkgconfig/libsystemd.pc
}
