# Maintainer: Nathan <ndowens@artixlinux.org>

pkgname=openvkl
pkgver=1.3.1
pkgrel=1.1
pkgdesc="IntelÂ® Open Volume Kernel Library"
arch=(x86_64)
url="https://www.openvkl.org/"
license=(Apache)
depends=(embree ispc glfw libglvnd imath openvdb rkcommon tbb zlib)
makedepends=(boost cmake)
source=(https://github.com/openvkl/openvkl/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
	embree.patch::https://github.com/openvkl/openvkl/commit/f4d7f220b4823e2cc1b26ba6c9f09107d6291edf.patch)
sha256sums=('c9cefb6c313f2b4c0331e9629931759a6bc204ec00deed6ec0becad1670a1933'
            '4c249aa99ba38d512ae867b78bb9aeda83e59a7919ead8f38659bf2b7cb39a4d')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../embree.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DOpenVDB_ROOT=/usr \
    -Wno-dev
  cmake --build build
}

check() {
  LD_LIBRARY_PATH="${PWD}"/build ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
