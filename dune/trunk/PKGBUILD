# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=3.4.1
pkgrel=1
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=(x86_64)
url="https://github.com/ocaml/dune"
license=(Apache)
depends=('glibc')
makedepends=('ocaml' 'ocaml-csexp' 'ocaml-pp')
optdepends=('ocaml: Dune standard library')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ffd72d2b86b1be0f7f80cd42e5a6e0fb75a89c3b138fd8533804a0d9cc51ca3c0e2cb38e08e71a38e1c9a55d20840d989fc0682efc43de27d4b77ac6042c3c7a')

_dune_release_pkgs=('dune' 'dune-action-plugin' 'dune-build-info' 'dune-configurator' 'dune-glob' 'dune-private-libs' 'dune-site' 'dyn' 'stdune' 'ordering' 'xdg' 'fiber')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # FS#74061
    ./configure --libdir /usr/lib/ocaml

    make dune.exe  # this runs `ocaml bootstrap.ml`, but keeps upstream's choice

    # dune build needs a comma separated list of packages
    local dune_release_pkgs='dummy'
    for _pkg in "${_dune_release_pkgs[@]}"; do
      dune_release_pkgs+=",${_pkg}"
    done
    dune_release_pkgs="${dune_release_pkgs#dummy,}"
    echo "Building packages: ${dune_release_pkgs}"

    ./dune.exe build -p "${dune_release_pkgs}" --profile dune-bootstrap
}

# Tests require a bunch of (currently) unpackaged dependencies
# check() {
#     cd "${srcdir}/${pkgname}-${pkgver}"
#     make test
# }

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # we need to install packages one by one
	for _pkg in "${_dune_release_pkgs[@]}"; do
      ./dune.exe install "${_pkg}" --destdir="${pkgdir}" --prefix="/usr" --libdir="$(ocamlfind printconf destdir)"
    done

    # Fix doc and man install
    install -d "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{doc,share/}
    mv "${pkgdir}"/usr/{man,share/}
    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s /usr/share/doc/pp/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
