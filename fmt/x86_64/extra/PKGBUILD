# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Mihai Bi≈üog <mihai.bisog@gmail.com>

pkgname=fmt
pkgver=9.1.0
pkgrel=1
pkgdesc='Open-source formatting library for C++'
arch=(x86_64)
url=https://fmt.dev
license=(BSD)
depends=(gcc-libs)
makedepends=(
  cmake
  doxygen
  git
  ninja
  npm
  python-pip
  python-virtualenv
)
provides=(libfmt.so)
_tag=a33701196adfad74917046096bf5a2aa0ab0bb50
source=(git+https://github.com/fmtlib/fmt.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  npm install less less-plugin-clean-css
  sed "s/'lessc',/'npx', 'lessc',/" -i fmt/doc/build.py
}

pkgver() {
  cd fmt
  git describe --tags
}

build() {
  artix-cmake -S fmt -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
  cmake --build build --target doc
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm 644 fmt/LICENSE.rst -t "${pkgdir}"/usr/share/licenses/fmt/
}

# vim: ts=2 sw=2 et:
