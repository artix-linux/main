# Maintainer: Nathan <ndowens@artixlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>

pkgname=ntp
_pkgname=ntp #-dev
_pkgver=4.2.8p15
pkgver=${_pkgver/p/.p}
pkgrel=3
pkgdesc='Network Time Protocol reference implementation'
url='http://www.ntp.org/'
license=('custom')
arch=('x86_64')
depends=('openssl' 'perl' 'libcap' 'libedit')
backup=('etc/ntp.conf')
source=("https://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-4.2/${_pkgname}-${_pkgver}.tar.gz"
        'ntp-4.2.8_p15-glibc-2.34.patch'
        'ntp.conf'
        'ntp.sysusers')
sha1sums=('e34e5b6f48c3ed1bbcfb03080dec1b8f91e19381'
          '1423b8ef72a3806ed74affc1973f66ed7cfa2afb'
          'ad1d6ee2e9aca64a84a7224c88bf6008ac6c69e1'
          'e8d1c895eabeb45b1a5f096fa5fc3c088eb3ee71')
options=('!emptydirs')

prepare() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"
	patch -Np1 -i ../ntp-4.2.8_p15-glibc-2.34.patch
}

build() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	./configure --prefix=/usr --libexecdir=/usr/lib --enable-linuxcaps --enable-ntp-signd
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"

	make DESTDIR="${pkgdir}" install

	install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
	install -Dm644 ../ntp.sysusers "${pkgdir}"/usr/lib/sysusers.d/ntp.conf
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -d -o 87 "${pkgdir}"/var/lib/ntp
	echo > "${pkgdir}/var/lib/ntp/.placeholder"
}
