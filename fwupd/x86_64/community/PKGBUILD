# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupd
pkgver=1.8.7
pkgrel=2
pkgdesc="Simple daemon to allow session software to update firmware"
arch=(x86_64)
url="https://github.com/fwupd/fwupd"
license=(LGPL)
depends=(libxmlb efivar python libsmbios libgusb
         polkit shared-mime-info tpm2-tss flashrom
         libjcat fwupd-efi gcab hicolor-icon-theme
         bluez gnutls
         libarchive.so libcurl.so libcbor.so
         libjson-glib-1.0.so libgudev-1.0.so libmm-glib.so
         libqmi-glib.so libprotobuf-c.so)
optdepends=(
    'udisks2: UEFI firmware upgrade support'
)
makedepends=(meson valgrind gobject-introspection gi-docgen
             python-cairo noto-fonts noto-fonts-cjk python-gobject vala
             bash-completion python-pillow help2man gnu-efi-libs)
checkdepends=(umockdev)
provides=(libfwupd.so)
backup=('etc/fwupd/daemon.conf'
        'etc/fwupd/redfish.conf'
        'etc/fwupd/remotes.d/dell-esrt.conf'
        'etc/fwupd/remotes.d/fwupd-tests.conf'
        'etc/fwupd/remotes.d/lvfs-testing.conf'
        'etc/fwupd/remotes.d/lvfs.conf'
        'etc/fwupd/remotes.d/vendor-directory.conf'
        'etc/fwupd/remotes.d/vendor.conf'
        'etc/fwupd/thunderbolt.conf'
        'etc/fwupd/uefi_capsule.conf'
)
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "https://github.com/fwupd/fwupd/commit/73a63b89a051dda3c149b16ffc0f4759e1611715.patch")
sha512sums=('374339178a1fbf85bc25b47077d0db351b3664a02ec9478c4a5381228125f3bd65ef0be9eb526574bd09ab665aadd6cd9e39f01ebbfebbd338ec191ba24c5a36'
            'SKIP'
            '19e9d2905789034832b679c3ac82fe53b947c585e94656a17e58c997ba3bfe502fbb308ca60c1cd35a6a2d730d9211d0a569549b98ffb47799882f67ff63a978')
b2sums=('1c4e8f369581f826a710274e0789fbcfe735ce58407d1c0f7472721895bf426e0ddf0fd3d014a35efe39726f8891faa7960aaa455482b435138ea116bddbc142'
        'SKIP'
        'ffeb81b6c6360333450c095243d797958ceaa679f759ccf96e13f3e286da959147dd33f5e26571f8ba6612198f1b268950b3f2c95740ee6aeab55d6359139284')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

prepare() {
    cd ${pkgname}-${pkgver}
    # fix lvfs-testing.conf containing @compression@ placeholder
    patch -Np1 < ../73a63b89a051dda3c149b16ffc0f4759e1611715.patch    
}


build() {
    artix-meson ${pkgname}-${pkgver} build \
        -D b_lto=false \
        -D docs=enabled \
        -D plugin_intel_spi=true \
        -D systemd=false \
	-D offline=false \
        -D elogind=true \
        -D supported_build=enabled \
        -D efi_binary=false
    meson compile -C build
}

check() {
    meson test -C build || :
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
    # Fixup mode to match polkit
    install -d -o root -g 102 -m 750 "${pkgdir}"/usr/share/polkit-1/rules.d
    # Remove the tests
    rm -r "${pkgdir}"/usr/share/installed-tests/
    mv "${pkgdir}"/usr/bin/{,fwupd-}dbxtool
    mv "${pkgdir}"/usr/share/man/man1/{,fwupd-}dbxtool.1
}
