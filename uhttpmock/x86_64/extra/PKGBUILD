# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=uhttpmock
pkgver=0.5.5
pkgrel=1
pkgdesc="HTTP web service mocking project for projects which use libsoup"
url="https://gitlab.freedesktop.org/pwithnall/uhttpmock"
depends=('glib2' 'libsoup')
makedepends=('meson' 'gtk-doc')
license=('LGPL2.1')
arch=(x86_64)
source=(https://gitlab.freedesktop.org/pwithnall/uhttpmock/-/archive/$pkgver/uhttpmock-$pkgver.tar.gz)
sha256sums=('61e812c33f15ecd840ebe729cf10727142c1ef0244b589fd82be7d1f9f19b8fe')

build() {
  meson build $pkgname-$pkgver \
    --prefix=/usr \
    -D introspection=false
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
