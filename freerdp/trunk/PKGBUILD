# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

_name=FreeRDP
pkgname=freerdp
pkgver=2.8.0
pkgrel=1
epoch=2
pkgdesc="Free implementation of the Remote Desktop Protocol (RDP)"
arch=(x86_64)
url="https://www.freerdp.com/"
license=(Apache)
depends=(dbus-glib glibc gstreamer gst-plugins-base-libs libcups libgssglue
libx11 libxcursor libxext libxdamage libxfixes libxkbcommon libxi libxinerama
libxkbfile libxrandr libxrender libxtst openssl pcsclite wayland)
makedepends=(alsa-lib cmake docbook-xsl ffmpeg icu krb5 libjpeg-turbo libpulse
libusb pam xmlto xorgproto)
provides=(libfreerdp2.so libfreerdp-client2.so libfreerdp-server2
libfreerdp-shadow2.so libfreerdp-shadow-subsystem2.so libwinpr2.so
libwinpr-tools2.so libuwac0.so)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz
  $pkgname-2.0.0-manpage_formatting.patch
)
sha512sums=('cec78dcc78ba02e6394d9831abae6cab619ef257ad32cb57fa0897f20040c476667aee06d943985c85ea989b2e7b30b17a6e1febfec07330d4deec71579c365a'
            'd960e042d1527b5d5721136b6b20fc36f65beafd010581ea5b908668537fe9fe622de6689a29c0274b0d6f1e513615f0d02e56c1d1d1e613d093e145d39af8d7')
b2sums=('95cc0e863e75560528d431626a340fa66e77b4cfaa4110949809e230cff15f4eb986204c232ac79e7b0af0e19956c0c01723eba3787533eb91f4fcffe483cc01'
        '8242fe56ba9cf5352d836f656645a46142c1aecea3e8200203117a0cccc226135591c16fa0a24cfa196ade7b703832ff6871a948fd3bbbcc1d9c6df9885dc360')

prepare() {
  # fix man page formatting:
  # https://bugs.archlinux.org/task/64814
  patch -d $_name-$pkgver -p1 -i ../$pkgname-2.0.0-manpage_formatting.patch
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=None \
        -DPROXY_PLUGINDIR=/usr/lib/freerdp2/server/proxy/plugins \
        -DWITH_DSP_FFMPEG=ON \
        -DWITH_FFMPEG=ON \
        -DWITH_PULSE=ON \
        -DWITH_CUPS=ON \
        -DWITH_PCSC=ON \
        -DWITH_ICU=ON \
        -DWITH_JPEG=ON \
        -DWITH_SERVER=ON \
        -DWITH_SWSCALE=ON \
        -DWITH_CHANNELS=ON \
        -DWITH_LIBSYSTEMD=OFF \
        -DWITH_CLIENT_CHANNELS=ON \
        -DWITH_SERVER_CHANNELS=ON \
        -DCHANNEL_URBDRC_CLIENT=ON \
        -Wno-dev \
        -B build \
        -S $_name-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libavcodec.so libavutil.so libicuuc.so libjpeg.so
  libpam.so libpulse.so libswscale.so libswresample.so
  libusb-1.0.so)

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $_name-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
