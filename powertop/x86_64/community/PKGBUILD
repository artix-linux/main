# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=powertop
pkgver=2.14
pkgrel=1
pkgdesc='A tool to diagnose issues with power consumption and power management'
arch=('x86_64')
url='https://01.org/powertop/'
license=('GPL2')
depends=('gcc-libs' 'libnl' 'ncurses' 'pciutils')
makedepends=('autoconf-archive' 'patch')
optdepends=('xorg-xset: for the --calibrate function')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/fenrus75/powertop/archive/v$pkgver.tar.gz
        $pkgname-$pkgver-undefined-symbol.patch)
sha256sums=('0e4cd0933a4f1fe04bfbf9d56efaabc841d9b04d391426f1a3469d6635a25f37'
            '02be52b5b48fb562ac27c90ac1fef3f1c94d5dba53229209bfc290c47ef274f0')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
# https://github.com/fenrus75/powertop/issues/84
  patch -Np0 -i "${srcdir}"/$pkgname-$pkgver-undefined-symbol.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
