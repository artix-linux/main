# Maintainer: artoo <artoo@artixlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=less
pkgver=608
pkgrel=2
epoch=1
pkgdesc='A terminal based program for viewing text files'
license=('GPL3')
arch=('x86_64')
url='https://www.greenwoodsoftware.com/less/'
depends=('glibc' 'ncurses' 'pcre2')
validpgpkeys=('AE27252BD6846E7D6EAE1DD6F153A7C833235259') # Mark Nudelman
source=("https://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::https://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.sig"
        less-CVE-2022-46663.patch::https://github.com/gwsw/less/commit/a78e1351113cef564d790a730d657a321624d79c.patch)
sha256sums=('a69abe2e0a126777e021d3b73aa3222e1b261f10e64624d41ec079685a6ac209'
            'SKIP'
            '3862ce1d6ee0aacefe75274f04afc00a38474d4cf2f6be96feb6aaa226c9bd98')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < ../less-CVE-2022-46663.patch
}

build() {
  cd $pkgname-$pkgver
  sh configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
