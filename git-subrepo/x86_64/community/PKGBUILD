# Maintainer: artoo <artoo@artixlinux.org>

pkgname=git-subrepo
pkgver=0.4.3
pkgrel=2
pkgdesc="Git Submodule Alternative"
arch=('x86_64')
url="https://github.com/ingydotnet/git-subrepo"
license=('MIT')
depends=('git' 'bash')
makedepends=('shellcheck')
optdepends=('shellcheck: linting')
source=(git+https://github.com/ingydotnet/git-subrepo.git#tag=$pkgver
        index-filter.patch::https://github.com/ingydotnet/git-subrepo/pull/436/commits/ca6a98f8ef9d714aebef4d2637d72ce78ddaaea1.patch)
sha256sums=('SKIP'
            'aa13f891a17c5b8562c793f4eb3c30c522d5a2046fcbaa3575f27e0f6230231f')

prepare(){
    cd ${pkgname}
    patch -Np1 -i ../index-filter.patch
}

check(){
    cd ${pkgname}
    make test || true
}

package() {
  cd "${pkgname}"

  # git-subrepo
  install -d "${pkgdir}/usr/lib/git-core/"
  make INSTALL_LIB="${pkgdir}/usr/lib/git-core" PREFIX=$"${pkgdir}/usr" install

  # Completions Bash and ZSH /!\ DOESN'T WORK /!\
  install -D -m0644 "share/completion.bash" \
	  "${pkgdir}/usr/share/bash-completion/completions/git-subrepo"
  install -D -m0644 "share/zsh-completion/_git-subrepo" \
	  "${pkgdir}/usr/share/zsh/site-functions/_git-subrepo"

  # License
  install -D -m644 License "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
