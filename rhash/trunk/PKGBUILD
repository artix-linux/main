# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: 謝致邦 <Yeking@Red54.com>
# Contributor: cantabile <cantabile dot desu at gmail dot com>
# Contributor: reMiND <detanator@gmail.com>

pkgname=rhash
pkgver=1.4.2
pkgrel=1

pkgdesc='Utility for verifying hash sums of files'
url='https://github.com/rhash/RHash'
license=('BSD')
arch=('x86_64')

depends=('glibc')

backup=('etc/rhashrc')

source=(rhash-$pkgver.tar.gz::https://github.com/rhash/RHash/archive/v$pkgver.tar.gz
        rhash-$pkgver.tar.gz.asc::https://github.com/rhash/RHash/releases/download/v$pkgver/v$pkgver.tar.gz.asc)

sha512sums=('41df57e8b3f32c93d8e6f2ac668b32aaa23eb2eaf90a83f109e61e511404a5036ea88bcf2854e19c1ade0f61960e0d9edf01f3d82e1c645fed36579e9d7a6a25'
            'SKIP')
# https://sourceforge.net/p/rhash/wiki/PublicKey/
validpgpkeys=('2875F6B1C2D27A4F0C8AF60B2A714497E37363AE')

prepare() {
  cd RHash-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --extra-cflags="$CFLAGS" --extra-ldflags="$LDFLAGS"
}

build() {
  cd RHash-$pkgver
  make
}

check() {
  cd RHash-$pkgver
  make test test-lib
}

package() {
  cd RHash-$pkgver

  # program
  make DESTDIR="$pkgdir" install

  # library, since the makefiles are still broken
  make -C librhash DESTDIR="$pkgdir" install-lib-headers install-lib-shared install-so-link

  # license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/rhash/LICENSE
}
