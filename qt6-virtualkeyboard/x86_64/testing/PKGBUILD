# Maintainer: artist <artist@artixlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-virtualkeyboard
_qtver=6.4.1
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Virtual keyboard framework'
depends=(qt6-declarative qt6-svg hunspell)
makedepends=(cmake ninja)
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/official_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz)
sha256sums=('c25fdd5043f590c0eb53a2a2f101c279355891a6dd2b4563baf4e5edba2d3b21')

build() {
  cmake -B build -S $_pkgfn -G Ninja  -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS \
    -DQT_FEATURE_vkb_arrow_keynavigation=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
