# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=fltk
pkgname=(fltk fltk-docs)
pkgver=1.3.8
pkgrel=1
pkgdesc="Graphical user interface toolkit for X"
arch=(x86_64)
license=(custom:FLTK)
url="https://www.fltk.org/"
makedepends=(alsa-lib cmake doxygen fontconfig libglvnd libjpeg-turbo libpng
mesa libx11 libxcursor libxfixes libxft libxinerama libxrender)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgbase}/${pkgbase}/archive/release-${pkgver}.tar.gz")
sha512sums=('197848d3b80a65cca936daf4f0b74609f0fe8332a4cd11af53385fb2aa45ad698b1e239a48732b118cd3cb189bc531711b72fb2eeeb85be887dc6c5a558fa4b3')
b2sums=('d0a963a38b62ea8cf1d5fe57fab6664fc64083ff41f17847d737f71bfff08578c682deefb9847a9ab5b93210fce2afbfc2020bdfcde714947beac9b3f7c84358')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  # fat-lto-objects is required for non-mangled .a files in fltk
  CFLAGS+=" -ffat-lto-objects"
  CXXFLAGS+=" -ffat-lto-objects"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DFLTK_LIBDIR=/usr/lib \
        -DOPTION_BUILD_SHARED_LIBS=ON \
        -DOPTION_BUILD_HTML_DOCUMENTATION=ON \
        -DOPTION_INSTALL_HTML_DOCUMENTATION=ON \
        -Wno-dev \
        -B build \
        -S "${pkgbase}-release-${pkgver}"
  make VERBOSE=1 -C build
  make VERBOSE=1 -C build/documentation html
}

package_fltk() {
  depends=(gcc-libs glibc hicolor-icon-theme libfontconfig.so
  libglvnd libjpeg-turbo libpng libx11 libxcursor libxext
  libxfixes libxft libxinerama libxrender)
  provides=(libfltk_images.so libfltk_gl.so libfltk_forms.so libfltk.so)
  options=(!docs staticlibs)

  make VERBOSE=1 DESTDIR="${pkgdir}/" install -C build
  install -vDm 644 "${pkgbase}-release-${pkgver}"/COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # remove man pages of examples
  rm -rvf "${pkgdir}/usr/share/man/man6"
  (
    cd "${pkgdir}"
    _pick fltk-docs usr/share/doc/${pkgname}/html
  )
}

package_fltk-docs() {
  pkgdesc+=" (documentation)"

  mv -v fltk-docs/* "$pkgdir"
  install -vDm 644 "${pkgbase}-release-${pkgver}"/COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
