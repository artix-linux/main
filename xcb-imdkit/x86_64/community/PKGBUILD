# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=xcb-imdkit
pkgver=1.0.3
pkgrel=1
pkgdesc="Input method development support for xcb"
arch=('x86_64')
url="https://github.com/fcitx/xcb-imdkit"
license=('GPL')
depends=('xcb-util' 'xcb-util-keysyms')
makedepends=('extra-cmake-modules' 'ninja' 'uthash' 'xorgproto')
source=("https://download.fcitx-im.org/fcitx5/xcb-imdkit/xcb-imdkit-$pkgver.tar.xz"{,.sig})
sha512sums=('df15dbe8363f89259f59b4afb81befd3be88173362f78c3b6cf0c7cdb2bdca75b352b779e0e7a91bb814a6afe59c4cc8acc946ce96721ca376438a9e8f3b4e80'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$pkgver
  ninja test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
