# Maintainer:
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=6.8
pkgrel=2
pkgdesc='GNU documentation system for on-line information and printed output'
arch=(x86_64)
url='https://www.gnu.org/software/texinfo/'
license=(GPL3)
groups=(base-devel)
depends=(ncurses gzip perl sh)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-install.hook
        texinfo-remove.hook
        texinfo-pt-br-crash.patch)
sha256sums=('8eb753ed28bca21f8f56c1a180362aed789229bd62fff58bf8368e9beb59fec4'
            'SKIP'
            '5409fc3aa8a35e8f3d901e96bc50c986f204d90ce6bface0a7d79d99b487f801'
            '913ca8aac84386399b0a83ed0f6b04b5e6322da62f5c1d7ed31e1050ed37c1a9'
            '73639d69b29ca8274225b16a2a0d51055f1ef76b4a65e9b5b65b5bb0a152f3c7')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

prepare() {
  patch -d $pkgname-$pkgver -p1 < texinfo-pt-br-crash.patch # Avoid info crash for Brazillian Portuguese
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 texinfo-{install,remove}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
