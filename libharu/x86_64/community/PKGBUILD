# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Auguste <auguste@gmail.com>
# Contributor: Jose Negron <josenj.arch@mailnull.com>

pkgname=libharu
pkgver=2.3.0
pkgrel=5
pkgdesc="C library for generating PDF documents"
arch=(x86_64)
url="https://github.com/libharu/libharu"
license=(custom:ZLIB)
depends=(libpng zlib)
makedepends=(cmake)
source=(${url}/archive/RELEASE_${pkgver//./_}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-157.patch::${url}/pull/157.patch
        ${pkgname}-158.patch::${url}/pull/158.patch)
sha512sums=('ebcabf1e8488e21185a231e97b4c16f9de742ae0ac2ebc7535b72b6b8e2045c2619bc6a94a820f7f923b0dfeceb217408f4e4c613ef3602104373cdf000d3f41'
            '3b9474c0dad5e8903cea6af1403d28ce405d33cc7086e116ac5b2f6d9c4274e9c4dcf85c5719b7cae95fe72511bc3c79d65dd6ef440c4b951367f7b8a409386a'
            'babf974b91e2b4c1abc8bdcf2a46f6a4b663fad8ff9af995c68a0a85a263c19cb4e13a9b90a058eba4f6a5c11a04e9ac1d43e223384652f3d398041a67fd9cb0')

prepare() {
  cd ${pkgname}-RELEASE_${pkgver//./_}
  patch -p1 < ../${pkgname}-157.patch
  patch -p1 < ../${pkgname}-158.patch
  sed 's|set(LIBHPDF_MINOR 2)|set(LIBHPDF_MINOR 3)|' -i CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-RELEASE_${pkgver//./_} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBHPDF_STATIC=OFF
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  # License
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  sed -n '102,120p' ${pkgname}-RELEASE_${pkgver//./_}/README > "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  # Remove some uneeded wrongly installed files
  rm "${pkgdir}"/usr/{CHANGES,INSTALL,README}
  rm -r "${pkgdir}"/usr/if
}
