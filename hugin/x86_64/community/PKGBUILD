# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2021.0.0
pkgrel=5
pkgdesc="Panorama photo stitcher"
arch=(x86_64)
url="http://hugin.sourceforge.net/"
license=(GPL)
depends=(wxwidgets-gtk3 boost-libs libtiff libpano13 libjpeg libpng openexr vigra
         exiv2 glew sqlite lcms2 lapack fftw glu libxi libxmu python
         lensfun enblend-enfuse)
makedepends=(cmake boost tclap mesa swig)
optdepends=('perl-image-exiftool: GPano tags support'
            'dcraw: RAW import using dcraw'
            'darktable: RAW import using darktable'
            'rawtherapee: RAW import using rawtherapee')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        xdg-data.patch)
sha256sums=('047aea8a7fa47844b34ef27c19d3b697e84939dcb1fdbbeb2c204621b66eead9'
            'c3eecff24cb12df44aa2bec923cd9c56f386d82d5efcae53fde1db6840a6be42')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../xdg-data.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_LAPACK=yes \
    -DBUILD_HSI=ON \
    -DUSE_GDKBACKEND_X11=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
