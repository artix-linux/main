# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-clp
pkgver=1.17.7
pkgrel=2
pkgdesc='COIN-OR linear programming solver'
arch=(x86_64)
url='https://github.com/coin-or/Clp'
license=(EPL)
groups=(coin-or)
depends=(coin-or-osi coin-or-asl suitesparse)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Clp/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('c4c2c0e014220ce8b6294f3be0f3a595a37bef58a14bf9bac406016e9e73b0f5')

build() {
  cd Clp-releases-$pkgver
  ./configure --prefix=/usr \
              --with-cholmod-lib='-lcholmod' \
              --with-cholmod-incdir=/usr/include \
              --with-amd-lib='-lamd' \
              --with-amd-incdir=/usr/include
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

check() {
  cd Clp-releases-$pkgver
  make test
}

package() {
  cd Clp-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
