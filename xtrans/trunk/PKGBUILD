# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xtrans
pkgver=1.4.0
pkgrel=4
pkgdesc="X transport library"
arch=('any')
license=('custom')
url="https://xorg.freedesktop.org/"
options=(!emptydirs)
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2{,.sig}
	xtrans-elogind.diff)
sha512sums=('4fea89a3455c0e13321cbefa43340016dbb59bdd0dbdb5b796c1a6d2a6b1fd63cf1327b769ab426286b9c54b32ec764a50cd2b46228e4e43b841bda6b94de214'
            'SKIP'
            '887cce7fe0023541855a1d908942e78d09fdf10f3452e20f89caf9d5a28aa73493892284fc2868852b7fdcaff55504e9bd873f9d8569e57e1ac8b7e7b5d1fd7a')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') #  "Keith Packard <keithp@keithp.com>"
validpgpkeys+=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i "$srcdir"/xtrans-elogind.diff
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr 
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
