# Maintainer: Nathan <ndowens@artixlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=java-batik
pkgver=1.15
pkgrel=1
pkgdesc='SVG library for Java.'
arch=('any')
url='https://xmlgraphics.apache.org/batik/'
license=('APACHE')
depends=('java-runtime' 'java-commons-io' 'java-commons-logging' 'java-xmlgraphics-commons')
makedepends=('java-environment' 'apache-ant' 'unzip')
source=("https://www.apache.org/dist/xmlgraphics/batik/source/batik-src-$pkgver.tar.gz"{,.asc})
sha512sums=('402ad231dbab4f15e28e58924fd1e289000585f662509115265264c5d8a5adbacb7b85c9bc07fa23d999c64c0b71c42ee5a789dc9c23b9c27d8d2e4efcf22bdd'
            'SKIP')
validpgpkeys=('9D5F9D9B3E21B585E8F4551082036989D56B5F17'  # Luis Bernardo
              '5C9A30FF22B2C02F30261C305B93F1DF7CDB6DEA') # Simon Steiner

build() {
  cd batik-$pkgver

  ant all-jar
}

package() {
  install -d "$pkgdir"/usr/share/java/batik
  install -m 644 "$srcdir"/batik-$pkgver/batik-$pkgver/lib/batik-all-$pkgver.jar "$pkgdir"/usr/share/java/batik/batik-all-$pkgver.jar

  ln -s batik-all-$pkgver.jar "$pkgdir"/usr/share/java/batik/batik-all.jar

  cp --no-preserve=ownership "$srcdir/batik-$pkgver/lib"/*.jar "$pkgdir"/usr/share/java/batik/
}

