# Maintainer: Nathan <ndowens@artixlinux.org>

pkgname=libqmi
pkgver=1.32.2
pkgrel=1
pkgdesc="QMI modem protocol helper library"
url="https://www.freedesktop.org/wiki/Software/libqmi/"
arch=(x86_64)
license=(GPL2)
depends=(libmbim libqrtr-glib libgudev)
makedepends=(gtk-doc meson git help2man gobject-introspection bash-completion)
provides=(libqmi-glib.so)
options=(debug)
_commit=c6b38cc2752873c0c6c1f8c472d9ddea2b34ec72  # tags/1.32.2^0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqmi.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libqmi
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libqmi
}

build() {
  artix-meson libqmi build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
