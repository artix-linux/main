# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.1.7
pkgrel=2
pkgdesc='iSCSI userland tools'
arch=('x86_64')
url='https://www.open-iscsi.com/'
license=('GPL')
makedepends=('udev')
depends=('libutil-linux' 'kmod' 'openssl' 'open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz"
        '0001-etc-install-system-unit-with-without-executable-bit.patch')
sha256sums=('d96761e47a69f8214c5fbd251d844f37961b14c3e437b63a15cc64f5b8cba2f0'
            '4958b838c548f6797ee25aea47b018a03be2ee11299ff41e9b56ad74f1121ead')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver}

  patch -Np1 < ../0001-etc-install-system-unit-with-without-executable-bit.patch
}

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make \
    NO_SYSTEMD=y \
    LIB_DIR=/usr/lib \
    RULESDIR=/usr/lib/udev/rules.d \
    SBINDIR=/usr/bin \
    user
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make -j1 \
    NO_SYSTEMD=y \
    DESTDIR="$pkgdir" \
    LIB_DIR=/usr/lib \
    RULESDIR=/usr/lib/udev/rules.d \
    SBINDIR=/usr/bin \
    install

  install -D -m0644 "$srcdir"/${pkgname}-${pkgver}/etc/iscsid.conf "$pkgdir"/etc/iscsi
  echo -n > "$pkgdir"/etc/iscsi/initiatorname.iscsi

  # copy docs
  mkdir -p "$pkgdir"/usr/share/doc/${pkgname}
  install -m0644 Changelog "$pkgdir"/usr/share/doc/${pkgname}
  install -m0644 README "$pkgdir"/usr/share/doc/${pkgname}
  
  # Remove systemd dir
  rm -rf "$pkgdir"/usr/lib/systemd
}
