# Maintainer: Torr <torr[at]artixlinux[dot]org>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=nawk
provides=("awk")
pkgver=20220122
pkgrel=1.2
pkgdesc="The one, true implementation of AWK"
url="https://github.com/onetrueawk/awk"
license=("custom")
arch=("x86_64")
depends=("glibc")
makedepends=(
	"make"
	"bison"
	"diffutils"
	"gcc"
)
source=(
	"$url/archive/refs/tags/$pkgver/${provides[0]}-$pkgver.tar.gz"
	"manpage_naming.patch"
)
sha256sums=(
	"720a06ff8dcc12686a5176e8a4c74b1295753df816e38468a6cf077562d54042"
	"fa1bade363896033a50fbdadc8fa2f490f85a044d11afcd537f317067c70d3b5"
)
install="setup.sh"

prepare() {
	cd "${provides[0]}-$pkgver"
	patch -Np1 -i ../manpage_naming.patch
}

build() {
	cd "${provides[0]}-$pkgver"
	# the final linking step doesn't honor LDFLAGS, so let's just throw it all
	# together into CFLAGS.
	make \
	CPPFLAGS="-DHAS_ISBLANK $CPPFLAGS" \
	CFLAGS="$CFLAGS $LDFLAGS"
}

check() {
	cd "${provides[0]}-$pkgver"
	./REGRESS
}

package() {
	cd "${provides[0]}-$pkgver"
	install -Dm 0755 a.out "$pkgdir/usr/bin/$pkgname"
	install -Dm 0644 ${provides[0]}.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
	install -Dm 0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
