# Maintainer: Nathan <ndowens@artixlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>

pkgname=nawk
pkgver=20220912
_commit=(9e248c317b88470fc86aa7c988919dc49452c88c) # master
pkgrel=1
pkgdesc='The one, true implementation of AWK'
url="https://github.com/onetrueawk/awk"
license=(custom)
arch=(x86_64)
options=(!makeflags)
depends=(glibc)
makedepends=(git)
source=("git+https://github.com/onetrueawk/awk.git#commit=${_commit}"
        'manpage-naming.patch')
sha256sums=('SKIP'
            'fa1bade363896033a50fbdadc8fa2f490f85a044d11afcd537f317067c70d3b5')

pkgver() {
 cd awk
 git log -n1 --pretty='format:%cd' --date=format:'%Y%m%d' ${_commit}
}

prepare() {
  cd awk
  patch -Np1 < "../${source[1]}"
}

check() {
  cd awk
  ./REGRESS
}

build() {
  cd awk
  # the final linking step doesn't honor LDFLAGS, so let's just throw it all
  # together into CFLAGS.
  make \
    "CPPFLAGS=-DHAS_ISBLANK $CPPFLAGS" \
    "CFLAGS=$CFLAGS $LDFLAGS"
}

package() {
  cd awk
  install -Dm0755 a.out "$pkgdir/usr/bin/$pkgname"
  install -Dm0644 awk.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
