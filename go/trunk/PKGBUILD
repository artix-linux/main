# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Daniel Martí <mvdan@mvdan.cc>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Rémy Oudompheng <remy@archlinux.org>
# Contributor: Andres Perera <andres87p gmail>
# Contributor: Matthew Bauer <mjbauer95@gmail.com>
# Contributor: Christian Himpel <chressie@gmail.com>
# Contributor: Mike Rosset <mike.rosset@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw@gmail.com>
# Contributor: John Luebs <jkluebs@gmail.com>
# Contributor: Torr <torr[at]artixlinux[dot]org>

pkgname=go
epoch=2
pkgver=1.18.4
pkgrel=1.3
pkgdesc='Core compiler tools for the Go programming language'
arch=(x86_64)
url='https://golang.org/'
license=(BSD)
makedepends=(
	"go"
	"git"
	"gcc"
	"iana-etc"  # For tests invoked on the <check> func.
)
replaces=(go-pie)
provides=(go-pie)
options=(!strip staticlibs)
_zsh_complition_commit=12a73d020073c949a0208b101911e244c61fa67f  # Date: 2022-7-11
source=(
	"https://go.dev/dl/go$pkgver.src.tar.gz"
	"https://raw.githubusercontent.com/zsh-users/zsh-completions/$_zsh_complition_commit/src/_golang"

	"https://manpages.debian.org/bullseye-backports/golang-go/go.1.en.gz"
	"https://manpages.debian.org/bullseye-backports/golang-go/go-build.1.en.gz"
	"https://manpages.debian.org/bullseye-backports/golang-go/go-clean.1.en.gz"
	"https://manpages.debian.org/bullseye-backports/golang-go/go-get.1.en.gz"
	"https://manpages.debian.org/bullseye-backports/golang-go/go-install.1.en.gz"
	"https://manpages.debian.org/bullseye-backports/golang-go/go-mod.1.en.gz"
)
sha256sums=(
	"4525aa6b0e3cecb57845f4060a7075aafc9ab752bb7b6b4cf8a212d43078e1e4"
	"a1af2f187b8e4cb4b66cc500884666f1763df74e3e6b50378725b8c2c871ce3d"

	"92139e6967defeefb9a998358ead58c4afd9c59f108dc3f18bcfb26ada33738f"
	"52ec8faba565ed1fd418d0e742a320e0cf3aae70d7ea24cd1e5aedaeeb7a6680"
	"7f634249407eb928e40b9046250ce7285d130e488bffe620da9b3e396fa0bbd1"
	"71804e565e1b19971f260e651784e5cfd5d19a6ff6370ac5cd4bff12e20fe1b8"
	"71fe8dbef726f9f677d6942862d1b3bca8a3f46d95a748e30da095cbd26da96f"
	"9d4f4ccbc6abb04cdc8fdc0bf3c1ff70953742f824f34ca17dad34fd54497234"
)

build() {
	export GOARCH=amd64
	export GOAMD64=v1 # make sure we're building for the right x86-64 version
	export GOROOT_FINAL=/usr/lib/go
	export GOROOT_BOOTSTRAP=/usr/lib/go

	cd "$pkgname/src"
	./make.bash -v
}

check() {
	export GO_TEST_TIMEOUT_SCALE=3

	cd $pkgname/src
	./run.bash --no-rebuild -v -v -v -k
}

package() {
	cd "$pkgname"

	install -d "$pkgdir/usr/bin" "$pkgdir/usr/lib/go" "$pkgdir/usr/share/doc/go" \
		"$pkgdir/usr/lib/go/pkg/linux_amd64_"{dynlink,race}

	cp -a bin pkg src lib misc api test "$pkgdir/usr/lib/go"
	# We can't strip all binaries and libraries,
	# as that also strips some testdata directories and breaks the tests.
	# Just strip the packaged binaries as a compromise.
	strip $STRIP_BINARIES "$pkgdir/usr/lib/go"{/bin/*,/pkg/tool/*/*}

	cp -r doc/* "$pkgdir/usr/share/doc/go"

	ln -sf /usr/lib/go/bin/go "$pkgdir/usr/bin/go"
	ln -sf /usr/lib/go/bin/gofmt "$pkgdir/usr/bin/gofmt"
	ln -sf /usr/share/doc/go "$pkgdir/usr/lib/go/doc"

	install -Dm644 VERSION "$pkgdir/usr/lib/go/VERSION"

	rm -rf "$pkgdir/usr/lib/go/pkg/bootstrap" "$pkgdir/usr/lib/go/pkg/tool/*/api"

	# TODO: Figure out if really needed
	rm -rf "$pkgdir"/usr/lib/go/pkg/obj/go-build

	install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm 644 -t "$pkgdir/usr/share/zsh/site-functions" ../_golang

	install -Dm 644 ../go.1.en.gz "$pkgdir/usr/share/man/man1/go.1.gz"
	install -Dm 644 ../go-build.1.en.gz "$pkgdir/usr/share/man/man1/go-build.1.gz"
	install -Dm 644 ../go-clean.1.en.gz "$pkgdir/usr/share/man/man1/go-clean.1.gz"
	install -Dm 644 ../go-get.1.en.gz "$pkgdir/usr/share/man/man1/go-get.1.gz"
	install -Dm 644 ../go-install.1.en.gz "$pkgdir/usr/share/man/man1/go-install.1.gz"
	install -Dm 644 ../go-mod.1.en.gz "$pkgdir/usr/share/man/man1/go-mod.1.gz"
}

