# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=blosc
pkgver=1.21.1
pkgrel=2
pkgdesc='A blocking, shuffling and loss-less compression library'
arch=('x86_64')
url='https://www.blosc.org'
license=('BSD')
depends=('snappy' 'zstd')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Blosc/c-blosc/archive/v${pkgver}.tar.gz")
sha512sums=('e06712ffaeaa6892415a6b01219221426c20af4191db6ca923301313daa8f5b0a83a7a3cd273f7013766349ace4e4869590e9c44bd892e57027ca45de53779e7')
b2sums=('a6a48d79fc8cb3df277e878a69361a5d12552be4b8ae50a4a95d2fe48d5b073c849724913a20c2834de7cf3cac7f288ddfec977ed1e4a1875fc3d21975907b44')

build() {
  cmake \
    -B build \
    -S "c-blosc-$pkgver" \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DBUILD_STATIC="OFF" \
    -DDEACTIVATE_SNAPPY="OFF" \
    -DPREFER_EXTERNAL_LZ4="ON" \
    -DPREFER_EXTERNAL_ZLIB="ON" \
    -DPREFER_EXTERNAL_ZSTD="ON"

  cmake --build build
}

check() {
  cd build

  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 \
    "c-blosc-$pkgver/LICENSES/BLOSC.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
