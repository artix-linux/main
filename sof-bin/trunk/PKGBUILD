# Maintainer: Nathan <ndowens@artixlinux.org>

pkgbase=sof-bin
pkgname=(sof-firmware sof-tools)
pkgver=2.2.4
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(x86_64)
license=(custom:BSD custom:ISC)
options=(debug)
_binver=v${pkgver}
source=(https://github.com/thesofproject/sof-bin/releases/download/$_binver/sof-bin-$_binver.tar.gz)
sha256sums=('ce8aaeba103aa56a82662552b0f33f33a859aa1008d8bfbc2c22f0ccfc8c6b3a')

package_sof-firmware() {
  options+=(!strip)

  local fwdir="$pkgdir/usr/lib/firmware/intel"
  mkdir -p "$fwdir"

  cd sof-bin-$_binver

  cp -at "$fwdir" sof-$_binver
  ln -sr "$fwdir"/{sof-$_binver,sof}

  cp -at "$fwdir" sof-tplg-$_binver
  ln -sr "$fwdir"/{sof-tplg-$_binver,sof-tplg}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-$_binver

  install -Dt "$pkgdir/usr/bin" tools-$_binver/*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 sts=-1 et:
