# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Daniel Bermond <dbermond@archlinux.org>

pkgname=amf-headers
pkgver=1.4.26
pkgrel=1
pkgdesc='Header files for AMD Advanced Media Framework'
arch=(any)
url=https://github.com/GPUOpen-LibrariesAndSDKs/AMF/
license=(MIT)
makedepends=(git)
_tag=5c78f194a396938ff0e72128712252151238e372
source=(git+https://github.com/GPUOpen-LibrariesAndSDKs/AMF.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd AMF

  git describe --tags | sed 's/^v//'
}

package() {
  install -dm 755 "${pkgdir}"/usr/include
  cp -dr --no-preserve=ownership AMF/amf/public/include "${pkgdir}"/usr/include/AMF
  install -Dm 644 AMF/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/amf-headers/
}

# vim: ts=2 sw=2 et:
