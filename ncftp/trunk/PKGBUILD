# Maintainer: qontinuum <qontinuum@artixlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ncftp
pkgver=3.2.6
pkgrel=5
pkgdesc="A set of free application programs implementing FTP"
url="https://www.ncftp.com/"
arch=('x86_64')
license=('custom')
depends=('ncurses')
makedepends=('inetutils')
source=("ftp://ftp.ncftp.com/ncftp/$pkgname-$pkgver-src.tar.xz"
        ncftp-reproducible.patch)
sha256sums=('5f200687c05d0807690d9fb770327b226f02dd86155b49e750853fce4e31098d'
            'd5df31ab43f1573a86a842fdc69d7ae84f20afd22c74336264cc4e1e39e2f44d')

prepare() {
  cd "${pkgname}-${pkgver}"
  # Make ncftp reproducible by not compiling in the kernel version
  patch -Np1 -i "${srcdir}/ncftp-reproducible.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  # FS#39211
  unset CPPFLAGS

  CPP=/usr/bin/cpp ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  install -d "${pkgdir}/usr/share"
  make prefix="${pkgdir}/usr" mandir="${pkgdir}/usr/share/man" install
  install -Dm644 doc/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
