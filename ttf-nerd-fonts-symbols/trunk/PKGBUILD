# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname=('ttf-nerd-fonts-symbols-1000-em' 'ttf-nerd-fonts-symbols-1000-em-mono' 'ttf-nerd-fonts-symbols-2048-em' 'ttf-nerd-fonts-symbols-2048-em-mono')
pkgbase=ttf-nerd-fonts-symbols
pkgver=2.2.1
pkgrel=3
pkgdesc="High number of extra glyphs from popular 'iconic fonts'"
arch=('any')
url=https://github.com/ryanoasis/nerd-fonts
license=('MIT')
provides=('nerd-fonts' 'ttf-font-nerd')
install=$pkgbase.install
source=("Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-1000-em%20Nerd%20Font%20Complete%20Mono.ttf"
        "Symbols-1000-em_Nerd_Font_Complete_Mono-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete%20Mono.ttf"
        "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete.ttf"
        "Symbols-2048-em_Nerd_Font_Complete_Mono-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/patched-fonts/NerdFontsSymbolsOnly/complete/Symbols-2048-em%20Nerd%20Font%20Complete%20Mono.ttf"
        "10-nerd-font-symbols-$pkgver.conf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/10-nerd-font-symbols.conf"
        "Nerd_Fonts-$pkgver-LICENSE::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/v$pkgver/LICENSE")
b2sums=('fe88f228c8b5ce6e1405462c6f18036cbb8669fc2e84988362f2b3345e8af7e0bd0f7da671c3cd046fc68e3999d77b29afa3a5037db3857b7a97f4d25e07c76a'
        'eb584868a70400b76a3649e2e82d600f3c271ab02ee637df23eba8310f4715bee192cb427d1de2d40aa158e2ce371801fb8c58cc1fa3e3ed9d8068ef3ae48ea5'
        'b2268d0b71afabe9faf00b3fdfdb037d7aa2f1aae8c295668763e21c681b3708f142f3be926e2ba933c9eaab25bc9cbc78475b3dd9984b5658a9f165f64f63af'
        'eb584868a70400b76a3649e2e82d600f3c271ab02ee637df23eba8310f4715bee192cb427d1de2d40aa158e2ce371801fb8c58cc1fa3e3ed9d8068ef3ae48ea5'
        'd73e826f433d42428bc467a734d3fd6280876b2fe23a1384730b2bc0fd4521a6d372654f835350d28b346e3cad8e7d6fa47dd11be8d3fae6e61d4707e7fd397b'
        '386dfcb5ead367bfc44fc55f7e94548c03bdb24ca088ab56f9609e7cdaa3eab9c56b548966feaf7312da606b645e9af35556fc212132733efcdbcc17397c808a')

_package() {
  case "$1" in
    ttf-nerd-fonts-symbols-1000-em)
      pkgdesc+=' (1000-em)'
      conflicts=('ttf-nerd-fonts-symbols-mono' 'ttf-nerd-fonts-symbols-1000-em-mono' 'ttf-nerd-fonts-symbols-2048-em' 'ttf-nerd-fonts-symbols-2048-em-mono')
      replaces=('ttf-nerd-fonts-symbols-mono')

      install -Dm644 "Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-1000-em Nerd Font Complete.ttf"
    ;;
    ttf-nerd-fonts-symbols-1000-em-mono)
      pkgdesc+=' (1000-em monospace)'
      conflicts=('ttf-nerd-fonts-symbols-1000-em' 'ttf-nerd-fonts-symbols-2048-em' 'ttf-nerd-fonts-symbols-2048-em-mono')

      install -Dm644 "Symbols-1000-em_Nerd_Font_Complete_Mono-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-1000-em Nerd Font Complete Mono.ttf"
    ;;
    ttf-nerd-fonts-symbols-2048-em)
      pkgdesc+=' (2048-em)'
      conflicts=('ttf-nerd-fonts-symbols' 'ttf-nerd-fonts-symbols-1000-em' 'ttf-nerd-fonts-symbols-1000-em-mono' 'ttf-nerd-fonts-symbols-2048-em-mono')
      replaces=('ttf-nerd-fonts-symbols')

      install -Dm644 "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-2048-em Nerd Font Complete.ttf"
    ;;
    ttf-nerd-fonts-symbols-2048-em-mono)
      pkgdesc+=' (2048-em monospace)'
      conflicts=('ttf-nerd-fonts-symbols-1000-em' 'ttf-nerd-fonts-symbols-1000-em-mono' 'ttf-nerd-fonts-symbols-2048-em')

      install -Dm644 "Symbols-2048-em_Nerd_Font_Complete_Mono-$pkgver.ttf" \
        "$pkgdir/usr/share/fonts/TTF/Symbols-2048-em Nerd Font Complete Mono.ttf"
    ;;
  esac

  install -Dm644 10-nerd-font-symbols-$pkgver.conf \
    "$pkgdir"/usr/share/fontconfig/conf.avail/10-nerd-font-symbols.conf
  install -Dm644 Nerd_Fonts-$pkgver-LICENSE \
    "$pkgdir"/usr/share/licenses/"$1"/LICENSE
}

for _pkgname in "${pkgname[@]}"; do
  eval "package_$_pkgname() { _package $_pkgname; }"
done
