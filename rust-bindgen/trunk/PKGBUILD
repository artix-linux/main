# Maintainer : Felix Yan <felixonmars@archlinux.org>

pkgname=rust-bindgen
pkgver=0.59.2
pkgrel=1
pkgdesc='Automatically generates Rust FFI bindings to C (and some C++) libraries'
url='https://github.com/rust-lang/rust-bindgen'
depends=('gcc-libs' 'clang')
makedepends=('rust')
arch=('x86_64')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rust-lang/rust-bindgen/archive/v$pkgver.tar.gz")
sha512sums=('a89d724c0696fc26ca9b0ab492c189527bbb47d551e3ac0d8c182d00d3d7a07ea6cc7e2d606b592058acfc4bafd84a5dce9592a4671d890074ae78ea75e780f1')

build() {
  cd $pkgname-$pkgver
  cargo build --release
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/bindgen "$pkgdir"/usr/bin/bindgen

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
