# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=rust-bindgen
pkgver=0.61.0
pkgrel=1
pkgdesc='Automatically generates Rust FFI bindings to C (and some C++) libraries'
url='https://github.com/rust-lang/rust-bindgen'
depends=('gcc-libs' 'clang')
makedepends=('cargo')
arch=('x86_64')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rust-lang/rust-bindgen/archive/v$pkgver.tar.gz"
        "cargo-lock.patch")
sha512sums=('df5688afc915f8f5629e1b404b1904f7c0dcae0798cd9f013a244e8b71d1a0e58c91cbf294076c309bc190e16239b19239ac0719a8260596c27443802b2708a9'
            '54b4b19d7a6f05da8d4a2d509dd96e353ae60418ce4469df4eeba08ecb9f42be2f491f1b6f43fa5fb1573d3a5946099ff1c02e83f3876a2e3265b2252818d5d8')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir/cargo-lock.patch"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --frozen
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/bindgen "$pkgdir"/usr/bin/bindgen

  install -Dm644 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
