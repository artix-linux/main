# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=rust-bindgen
pkgver=0.63.0
pkgrel=1
pkgdesc='Automatically generates Rust FFI bindings to C (and some C++) libraries'
url='https://github.com/rust-lang/rust-bindgen'
depends=('gcc-libs' 'clang')
makedepends=('cargo')
arch=('x86_64')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rust-lang/rust-bindgen/archive/v$pkgver.tar.gz")
sha512sums=('b43c415c4ac55c0cf8ccba6a21b945e890321ff3c11a3afacfd8c724d83ee6e21066a7c4004727eb4d74795fcea320b6e668ce022310c050ad031633a00d1519')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --frozen
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/bindgen "$pkgdir"/usr/bin/bindgen
  install -Dm644 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
