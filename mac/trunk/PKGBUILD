# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=7.56
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('gcc-libs')
makedepends=('python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_SDK_${_pkgver}.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('e69afcc6cc4594f40e28543355a99d05efb40b5a1438f53fcb61bc6ffe1a504a0efcb1e44f7477bb67588a9cb1e1c3e25c0bb3217599b84e64cc85772a65fe89'
            'ae842550b52e6dd7281acf254c9b4457a69a33bf7e8229780df990915048c44a91172b59c47e5f2b5e51dfae9f0dbc1916161d89689badfc59378c22a2a2347b')
b2sums=('2596aaf218ef518e4f4490ec5ae6846eaafd6aedab341c7675cfe3c0353feb93b5e4b2522d533ecd464bd52e1ae2391d8b302d9bea296cbeb3c79a07609647fb'
        'eb2ff25d3114cc18005816a8a8a3809fd28ecb9be0823d9ce6f1bf58120830d4623fa87ba71a38d7f25d354daf7575f819385bacb679f307e5373d4c4ea8973e')

build() {
  make -C Source/Projects/NonWindows

  # generate license
  html2text --ignore-links mac-7.56-license.html \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  make DESTDIR="$pkgdir" prefix=/usr install -C Source/Projects/NonWindows

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
