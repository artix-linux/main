# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Andrew Sun  <adsun701 at gmail dot com>
# Contributor: Kritias     <theodoridisgr at gmail dot com>
# Contributor: sudokode    <sudokode at gmail dot com>
# Contributor: Allan McRae <allan at archlinux dot org>

pkgname=libisl
pkgver=0.24
pkgrel=4
pkgdesc='Library for manipulating sets and relations of integer points bounded by linear constraints'
arch=('x86_64')
url='https://libisl.sourceforge.io'
license=('MIT')
depends=('gmp')
provides=('isl' 'libisl.so')
replaces=('isl')
options=(staticlibs)
source=("https://libisl.sourceforge.io/isl-${pkgver}.tar.xz")
b2sums=('39cbfd18ad05778e3a5a44429261b45e4abc3efe7730ee890674d968890fe5e52c73bc1f8d271c7c3bc72d5754e3f7fcb209bd139e823d19cb9ea4ce1440164d')

build() {
  cd "${srcdir}"/${pkgname#lib}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}"/${pkgname#lib}-${pkgver}
  make check || true
}

package() {
  cd "${srcdir}"/${pkgname#lib}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/gdb/auto-load/usr/lib/
  mv "${pkgdir}"/usr/lib/libisl.so.*-gdb.py "${pkgdir}"/usr/share/gdb/auto-load/usr/lib/
  
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
