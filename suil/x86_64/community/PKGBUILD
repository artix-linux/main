# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=suil
pkgname=(suil suil-docs)
pkgver=0.10.16
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=(x86_64)
url="https://drobilla.net/software/suil.html"
license=(ISC)
makedepends=(doxygen gtk3 lv2 meson python-sphinx python-sphinx-lv2-theme qt5-base qt5-x11extras)
options=(debug)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('9b1dac07b017934a93f70d514ec477785683e7ed5949b62a2c4ad139c1e84265399f54d3140dcf0e542d20a0919286aacb52103c91e2f6fc7ebd707db2d98ac4'
            'SKIP')
b2sums=('614217013b39215f4b6d9414451a55fbca42dd038c6bc961d3769660707bf2da45432c97628edef8dacb8630437bcb304d10616ced2fd9cf9a6a4d094f196f6c'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  artix-meson $pkgname-$pkgver build -D gtk2=disabled
  meson compile -C build
}

check() {
  meson test -C build
}

package_suil() {
  depends=(gcc-libs glibc)
  optdepends=(
    'glib2: support for wrapping X11 and Qt5 in GTK3'
    'gtk3: support for wrapping X11 and Qt5 in GTK3'
    'libx11: support for wrapping X11 directly and in GTK3'
    'qt5-base: support for wrapping X11 in Qt5 and Qt5 in GTK3'
    'qt5-x11extras: support for wrapping X11 in Qt5'
  )
  provides=(libsuil-0.so)

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,PACKAGING,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_suil-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/$pkgbase"{-0,}
  find "$pkgdir" -type f -iname "*.buildinfo" -delete
}
