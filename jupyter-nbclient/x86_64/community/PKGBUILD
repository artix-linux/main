# Maintainer: qontinuum <qontinuum@artixlinux.org>

pkgname=jupyter-nbclient
pkgver=0.6.5
pkgrel=1
pkgdesc="A tool for running Jupyter Notebooks in different execution contexts."
arch=('any')
url="https://github.com/jupyter/nbclient"
license=('BSD')
depends=('python-nest-asyncio' 'python-traitlets' 'python-async_generator' 'python-jupyter_client' 'jupyter-nbformat' 'python-testpath')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest-asyncio' 'python-xmltodict' 'jupyter-nbconvert' 'python-ipywidgets')
source=("https://github.com/jupyter/nbclient/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c2777bc83ea87565e5dcc83b903d2a1cf61c00ca0912838e83dc29b295aa6e4e')

build() {
  cd nbclient-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbclient-$pkgver
  pytest -v
}

package() {
  cd nbclient-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
