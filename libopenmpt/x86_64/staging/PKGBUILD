# Maintainer: Nathan Owens <ndowens@artixlinux.org>

pkgname=libopenmpt
pkgver=0.6.5
pkgrel=2
pkgdesc="A library to render tracker music to a PCM audio stream"
arch=(x86_64)
url="https://lib.openmpt.org/libopenmpt/"
license=(BSD)
depends=(
  flac libFLAC.so
  gcc-libs
  glibc
  libogg libogg.so
  libpulse libpulse.so libpulse-simple.so
  libsndfile libsndfile.so
  libvorbis libvorbis.so libvorbisfile.so
  mpg123 libmpg123.so
  portaudio libportaudio.so
)
makedepends=(autoconf-archive doxygen help2man)
# TODO: remove provides/replaces on openmpt123 after 2022-05-01
provides=(libopenmpt.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://lib.openmpt.org/files/libopenmpt/src/$pkgname-$pkgver+release.autotools.tar.gz)
sha512sums=('eb59ddb22fa07f216c76138c022187c76065be56d51520ed0dcf6f77638aaf832a803f63a398ca8d47bd368aee24dd425242017136c299fade6fd98b9d7b628c')
b2sums=('f04c3df0322f336bd9e90edbbac4fd0532a41fc1ff17c11a20371946bd9ccdc50c0f8adbf92858516d453213dc04bcc07c08a72739458966aba5dfe37a955b8a')

prepare() {
  cd $pkgname-$pkgver+release.autotools
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver+release.autotools
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver+release.autotools
}

package() {
  cd $pkgname-$pkgver+release.autotools
  make DESTDIR="$pkgdir" install
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
