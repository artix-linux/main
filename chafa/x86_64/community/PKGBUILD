# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.10.3
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('imagemagick' 'libxslt')
makedepends=('gtk-doc')
license=("LGPL")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.asc})
sha512sums=('29bcd5315a1c5bf08b214b9b58fabc64340e995a383b88e5f8245ba130b0506e4d13c34878d6cb32d1493fa1b8326520cbd176aeef22d932c72ede0430a38fda'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
