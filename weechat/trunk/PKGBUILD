# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=3.7
pkgrel=1
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoctor' 'cmake' 'aspell' 'guile' 'lua' 'perl' 'python'
             'ruby' 'tcl')
optdepends=('aspell: spellchecker support'
            'guile: support for guile scripts'
            'lua: support for lua scripts'
            'perl: support for perl scripts'
            'python: support for python scripts'
            'ruby: support for ruby scripts'
            'tcl: support for tcl scripts')
source=("https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('56a0ff3748c4c2962c1d1793bd3830e01f200ab650097b676333bdcd9ec3dd98b422801ad873af2f681be6ab5c39e465c2b47cbe8ea94aa810a7fa034326f1eb'
            'SKIP')
b2sums=('84da2316b6e54e84bd05222b446f2a4e50f18871472cdfed75cc9a696fc01b760d81176ee66745fefa6047f7e03b2c3df2b9298c38552ea0b9afd6e90bdbb198'
        'SKIP')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8') # WeeChat (signing key) <webmaster@weechat.org>

# cmake does not correctly handle CPPFLAGS, so kludge it in anyway:
# https://gitlab.kitware.com/cmake/cmake/-/issues/12928
CFLAGS+=" ${CPPFLAGS}"

build() {
  mkdir -p build
  cd build
  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON \
        -DENABLE_JAVASCRIPT=OFF \
        -DENABLE_PHP=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
