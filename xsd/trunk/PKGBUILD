# Maintainer:
# Contributor: kevku <kevku@gmx.com>

pkgname=xsd
pkgver=4.0.0
_pkgver=4.0.0+dep
pkgrel=4.1
pkgdesc="An open-source, cross-platform W3C XML Schema to C++ data binding compiler"
arch=('x86_64')
url="https://www.codesynthesis.com/products/xsd"
license=('GPL2')
depends=('xerces-c')
source=("https://www.codesynthesis.com/download/${pkgname}/4.0/${pkgname}-${_pkgver}.tar.bz2"
#         "xsdcxx.patch"
        0001-xsd_xsdcxx-rename.patch
        0100-C++Parser_Expat_support.patch
        0110-xerces-c3.2.patch
        0002-missing-include.patch
        0105-Fix_path_handling_bug.patch)
sha256sums=('eca52a9c8f52cdbe2ae4e364e4a909503493a0d51ea388fc6c9734565a859817'
            '0d0dafab1653be7e372784ae72f8678eb849f45a26ad905958ebcd8840002502'
            'f3e984fdc1cae51e2394170c69f6d0c37e1e82182d8b7b202e458ec0afcf2e1d'
            'bd228182441245d3572ec6f64c77fe622d4fc99857a2a4d1f72bda461ba43475'
            '3975adcd90c26ce5dd38e7190ad9db2061378edb064dc7200f9783491005289a'
            'be445dd180038df42fa4610737406e4405f4517311fe726e993a9fc8914f5247')

prepare() {
  cd ${pkgname}-${_pkgver}
#   patch -p1 -i "${srcdir}/xsdcxx.patch"
  patch -Np1 -i ../0001-xsd_xsdcxx-rename.patch
  patch -Np1 -i ../0002-missing-include.patch
  patch -Np1 -i ../0100-C++Parser_Expat_support.patch
  patch -Np1 -i ../0105-Fix_path_handling_bug.patch
  patch -Np1 -i ../0110-xerces-c3.2.patch

}

build() {
  cd ${pkgname}-${_pkgver}
  make CXXFLAGS="${CXXFLAGS}"
}

package() {
  cd ${pkgname}-${_pkgver}
  make install_prefix="${pkgdir}/usr" install

  # Fix conflicts with mono
  mv "${pkgdir}"/usr/bin/xsd{,cxx}
  mv "${pkgdir}"/usr/share/man/man1/xsd{,cxx}.1
}
