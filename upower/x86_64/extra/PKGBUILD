# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.20
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
url="https://upower.freedesktop.org"
arch=(x86_64)
license=(GPL)
depends=(libimobiledevice libgudev)
makedepends=(docbook-xsl gobject-introspection python git gtk-doc meson)
checkdepends=(python-{dbus,dbusmock,gobject} umockdev)
backup=(etc/UPower/UPower.conf)
options=(debug)
_commit=3f2eabb4d1f82bb8ca4ee357e5232cb4237fdc90  # tags/v0.99.20^0
source=("git+https://gitlab.freedesktop.org/upower/upower.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd upower
  git describe --tags | sed -e 's/^v\|^UPOWER_//;s/_/\./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd upower
}

build() {
  artix-meson -D systemdsystemunitdir=no upower build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so)
  provides+=(libupower-glib.so)

  meson install -C build --destdir "$pkgdir"
}
