# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=libutf8proc
pkgver=2.7.0
_sover=2.5.0
pkgrel=2
pkgdesc='C library for processing UTF-8 encoded Unicode strings'
arch=(x86_64)
url='https://github.com/JuliaStrings/utf8proc'
license=(custom)
makedepends=(cmake git ninja)
source=("git+$url#commit=8ca6144c85c165987cb1c5d8395c7314e13d4cd7" # tag: v2.7.0
        libutf8proc.pc.in)
b2sums=('SKIP'
        '27cfd2086b63b0d9b08720d966d760e4237734a85a293eab81065f49857aa11ed57682a4bd26e4081701d34e61c156170b65bbf38f19387539f5472d0d94db23')

prepare() {
  sed "s#@VERSION@#$pkgver#" libutf8proc.pc.in > libutf8proc.pc
}

build() {
  cmake -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D BUILD_SHARED_LIBS=ON \
    -G Ninja \
    -S utf8proc
  ninja -C build
}

package() {
  # The install command does not work for libutf8proc
  #DESTDIR="$pkgdir" ninja -C $pkgname-$pkgver/build install

  cd utf8proc
  install -Dm644 utf8proc.h "$pkgdir/usr/include/utf8proc.h"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  install -Dm644 "$srcdir/libutf8proc.pc" \
    "$pkgdir/usr/lib/pkgconfig/libutf8proc.pc"
  install -Dm644 ../build/libutf8proc.so.$_sover \
    "$pkgdir/usr/lib/libutf8proc.so.$_sover"
  ln -s /usr/lib/libutf8proc.so.$_sover "$pkgdir/usr/lib/libutf8proc.so"
  ldconfig -n "$pkgdir/usr/lib"
}
