# Maintainer: Nathan Owens <ndowens@artixlinux.org>

pkgname=wpebackend-fdo
pkgver=1.14.0
pkgrel=1
pkgdesc="Freedesktop.org backend for WPE WebKit"
url="https://wpewebkit.org"
arch=(x86_64)
license=(custom)
depends=(libwpe glib2 libegl wayland libepoxy)
makedepends=(mesa wayland-protocols git meson)
options=(debug)
_commit=966b37dc908df07fc7c6a49ccd103edaa6c17dab  # tags/1.14.0^0
source=("git+https://github.com/Igalia/WPEBackend-fdo#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

pkgver() {
  cd WPEBackend-fdo
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd WPEBackend-fdo
}

build() {
  artix-meson WPEBackend-fdo build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libwpe-1.0.so libg{lib,object,io}-2.0.so)
  provides+=(libWPEBackend-fdo-1.0.so)

  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 WPEBackend-fdo/COPYING
}

# vim:set sw=2 et:
