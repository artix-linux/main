# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Martin HÃ¤ger <martin.haeger@gmail.com>

pkgname=liboauth
pkgver=1.0.3+r15+gac4cf3a
pkgrel=1
epoch=1
pkgdesc="C library implementing OAuth Core RFC 5849"
url="https://github.com/x42/liboauth"
arch=(x86_64)
license=(MIT)
depends=(curl nss)
makedepends=(git)
_commit=ac4cf3a6cccd2b8496fd5e53483811a6b519f2c2  # master
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  autoreconf -fvi
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --enable-nss
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING.MIT
}
