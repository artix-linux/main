# Maintainer: Nathan <ndowens@artixlinux.org>

pkgname=prometheus
pkgver=2.41.0
pkgrel=1

pkgdesc='An open-source systems monitoring and alerting toolkit'
url='https://prometheus.io'
arch=('x86_64')
license=('Apache')

depends=('glibc')
makedepends=('go' 'git' 'npm' 'nodejs' 'yamllint' 'typescript' 'yarn')

backup=('etc/prometheus/prometheus.yml')
options=(!lto)

source=("prometheus-v$pkgver.tar.gz::https://github.com/prometheus/prometheus/archive/v$pkgver.tar.gz"
        prometheus.sysusers)

sha256sums=('1ef8f4ac5d6863f3d2ffbc70bbc900ef3e25e8353ae306aa05fdb7509a667f4c'
            '2747fabb4e56b808361eb7dd7acf9729ab8973d1ebe2f857dd56f6c71f71e45f')

build() {
  cd prometheus-$pkgver

  LDFLAGS="-extldflags $LDFLAGS \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$pkgver \
    -X github.com/prometheus/common/version.Branch=tarball \
    -X github.com/prometheus/common/version.BuildUser=someone@builder \
    -X github.com/prometheus/common/version.BuildDate=$(date -u '+%Y%m%d-%H:%M:%S' --date=@${SOURCE_DATE_EPOCH})"

  cd $srcdir/prometheus-$pkgver/web/ui
  GOOS= GOARCH= go generate -x -v

  cd $srcdir/prometheus-$pkgver
  # Build the react app
  make ui-install # run install first as otherwise the makefile has a race condition......
  make assets

  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external $LDFLAGS" \
    ./cmd/prometheus
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external $LDFLAGS" \
    ./cmd/promtool
}

check() {
  cd prometheus-$pkgver

  GODEBUG=x509sha1=1 go test -short ./... || :
}

package() {
  install -Dm644 prometheus.sysusers "$pkgdir"/usr/lib/sysusers.d/prometheus.conf

  cd prometheus-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin prometheus promtool
  install -Dm640 -g210 -t "$pkgdir"/etc/prometheus documentation/examples/prometheus.yml
  install -dm750 -o210 -g210 "$pkgdir"/var/lib/prometheus

  # Web
  install -dm755 "$pkgdir"/usr/share/prometheus/web/ui
  cp -R web/ui/static "$pkgdir"/usr/share/prometheus/web/ui/

  # Examples
  install -Dm644 -t "$pkgdir"/usr/share/doc/prometheus/examples documentation/examples/prometheus*.yml
  cp -R consoles console_libraries "$pkgdir"/usr/share/doc/prometheus/examples

}
