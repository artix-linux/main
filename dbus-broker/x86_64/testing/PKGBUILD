# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: David Herrmann <dh.herrmann@gmail.com>

pkgname=dbus-broker
pkgver=32
pkgrel=1
pkgdesc="Linux D-Bus Message Broker"
url="https://github.com/bus1/dbus-broker/wiki"
arch=(x86_64)
license=(Apache)
depends=(expat audit)
makedepends=(meson python-docutils)
options=(debug)
source=(https://github.com/bus1/dbus-broker/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('bea7f653e7251063c5f427e9e3f93562d38a0d8667ae6d49fb56f113605985de')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  artix-meson $pkgname-$pkgver build \
    -D audit=true \
    -D docs=true \
    -D launcher=false \
    -D linux-4-17=true \
    -D system-console-users=gdm,sddm,lightdm,lxdm
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
