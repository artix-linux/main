# Maintainer: Qontinuum <qontinuum@artixlinux.org>

pkgname=aws-c-sdkutils
pkgver=0.1.3
pkgrel=1
pkgdesc='AWS SDK utility library'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-sdkutils'
license=(Apache)
depends=(aws-c-common)
makedepends=(cmake)
source=(aws-c-sdkutils-$pkgver.zip::https://github.com/awslabs/aws-c-sdkutils/archive/v$pkgver.zip)
sha256sums=('9a02ba92e4d29aebbf945971321fa9b20eef567c23ec87f873c925166f2b7733')

build() {
  cd aws-c-sdkutils-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-sdkutils-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-sdkutils-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
