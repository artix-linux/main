# Maintainer: Nathan Owens <ndowens@artixlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gsasl
pkgver=2.0.1
pkgrel=1
pkgdesc="Simple Authentication and Security Layer framework and a few common SASL mechanisms"
arch=('x86_64')
url="https://josefsson.org/gsasl/"
license=('GPL')
depends=('gnutls' 'libidn' 'krb5' 'libgcrypt')
source=(https://ftp.gnu.org/gnu/gsasl/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('01c6f6bd9f986c942a25b89fee0052aef8c10bf914ead29983abdf0cc8fcaa7223fd9d9eeafb4be07e4bc318f087f6f6258facaaeb7f83bca8de512406812be5'
            'SKIP')
validpgpkeys=('B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE'   # Simon Josefsson <simon@josefsson.org>
              '9AA9BDB11BB1B99A21285A330664A76954265E8C')  # Simon Josefsson <simon@josefsson.org>
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-gssapi-impl=mit
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
