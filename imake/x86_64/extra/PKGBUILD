# Maintainer: Nathan <ndowens@artixlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=imake
pkgver=1.0.9
pkgrel=1
pkgdesc="X.Org imake program and related utilities"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xorgproto' 'perl' 'xorg-util-macros' 'xorg-font-util')
optdepends=('perl: for mkhtmlindex')
options=('debug')
source=(https://xorg.freedesktop.org/archive/individual/util/imake-${pkgver}.tar.xz
        https://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.3.tar.bz2
        https://xorg.freedesktop.org/releases/individual/util/lndir-1.0.3.tar.bz2
        https://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.6.tar.bz2
        https://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.7.tar.bz2{,.sig}
        linuxconfig.patch
        LICENSE)
sha512sums=('e7587cf80369c8f4fcc1207c13bcb74a53d838b504ac8a7f3b98fb8f5f595cc7b8e5e9fad50cadcf40cf27bd02ef4eae7fe458854bbed7bea27d474d66520644'
            '563b2897770a4df2792ea3eae8183a1f9e78192b7efc9c8296ee68d1cba6e72fa962a88e3251bb780df776e2aa7e80d74d5096675c61526c232aefa4f9e6a5fc'
            'c9f03c825c627935abe399bf8292bbf69304cb9e9d65a4c860b49e32e2ed4dde25bf658a5ab2116cd215381d0a52f464a8a53469c27adddbd9fa2bfd135a7289'
            'eb0e8ee8ab7515806d7d98a060cdcd392ec38e4225e5e05db26daa6651f185d0b764aab403edeea904d4d23211b1f7db20daeee6eafce8abe65225784ed0aa42'
            '0dd73b9db9fc16bddc94ef23f11434be6ea2f8e0c7bc5ef3c3bfe6749e13843f88d397eed9f980bbbe9e74147172326bd3c3ccb7adedc9150302756e4b07191e'
            'SKIP'
            'e2a42d60ca39a14ac7944af04eb76dfe9b194cfad8d853730a29aa43f93f2b4acce8d6e8d7a36901b201822c8c5879093fadb6e63f448f5915878909e3797735'
            '1ded8ef51405e3b0144a633b612270cb1c71e7365852653284a587972f68a390252ca73350517bac621006cab6410745974a960d9fd0d3f76dcb9face69aada9')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

prepare() {
  cd xorg-cf-files-1.0.7
  patch -p1 -i ../linuxconfig.patch
}

build() {
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      autoreconf -fi
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
