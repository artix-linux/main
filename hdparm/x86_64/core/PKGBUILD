# Maintainer: artoo <artoo@artixlinux.org>

pkgname=hdparm
pkgver=9.64
pkgrel=1
pkgdesc="A shell utility for manipulating Linux IDE drive/driver parameters"
arch=(x86_64)
depends=('glibc')
source=(https://downloads.sourceforge.net/sourceforge/hdparm/${pkgname}-${pkgver}.tar.gz
        pass-ldflags.patch)
license=('BSD')
url="https://sourceforge.net/projects/hdparm/"
sha512sums=('c561825143d8872cc2940bd6ba72162f91792bd63b6a8511058e79cc96feb6a4a9d22275581ef2eefab64a4035587b3a63175ccc58fa46f1059b4680d9d58ec4'
            '4103276304a858a9540ce173f22c5eb64cadac36b5cd38d9396d63fa721a579e16d4d0f84e3c627d0620c42815c437abf14c9657fdb1a28e8263a52a0bb26c4f')

prepare() {
  cd ${pkgname}-${pkgver}
  # Enable FULL RELRO
  patch -Np1 -i "$srcdir"/pass-ldflags.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  # install
  mkdir -p "${pkgdir}"/usr/bin
  make DESTDIR="${pkgdir}" sbindir=/usr/bin install
  install -m755 contrib/idectl "${pkgdir}"/usr/bin
  install -m755 contrib/ultrabayd "${pkgdir}"/usr/bin

  install -D -m 0644 $srcdir/$pkgname-$pkgver/wiper/README.txt "$pkgdir"/usr/share/doc/wiper/README.txt
  install -D -m 0755 $srcdir/$pkgname-$pkgver/wiper/wiper.sh "$pkgdir"/usr/bin/wiper.sh

  #install license file
  install -D -m 644 LICENSE.TXT "$pkgdir"/usr/share/licenses/hdparm/LICENSE.TXT
}
