# Maintainer: Cory Sanin <corysanin@artixlinux.org>

pkgname=libvips
pkgver=8.13.1
pkgrel=2
pkgdesc="A fast image processing library with low memory needs"
arch=('x86_64')
license=('LGPL')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'libexif' 'libgsf' 'libimagequant' 'librsvg' 'libwebp' 'libxml2' 'openexr'
         'orc' 'pango' 'libcgif')
# Adding 'highway' here temporarily for FS#73931
makedepends=('gobject-introspection' 'libheif' 'libjxl' 'imagemagick' 'openslide'
             'poppler-glib')
optdepends=('libheif: for heif module'
            'imagemagick: for magick module'
            'openslide: for openslide module'
            'poppler-glib: for poppler module'
            'libjxl: for jxl module')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.gz")
sha512sums=('0ec72216ff7079fd61b36c04639686e0fcf5a40590a20f539cdf39b3c6c78b3ecda3da12c0dc51a7b696d57845f11a02e81e1f4458bd9b2e3b1a7423cc9e0620')

build() {
  cd vips-$pkgver
  ./configure --prefix=/usr --disable-static
# Fix overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd vips-$pkgver
  make DESTDIR="$pkgdir" install
}
