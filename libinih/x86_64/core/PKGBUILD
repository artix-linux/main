# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alberto Oporto Ames <otreblain@gmail.com>

pkgname=libinih
pkgver=55
pkgrel=2
pkgdesc='A simple .INI file parser written in C'
arch=(x86_64)
url=https://github.com/benhoyt/inih
license=(BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
)
provides=(
  libinih.so
  libINIReader.so
)
_tag=f7f69c6cff2681d84bae371130b4a018cb2171e6
source=(git+https://github.com/benhoyt/inih.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd inih
  git describe --tags | sed 's/^r//'
}

build() {
  artix-meson inih build \
    -Ddefault_library=shared \
    -Ddistro_install=true \
    -Dwith_INIReader=true
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm 644 inih/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libinih/
}

# vim: ts=2 sw=2 et:
