# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=augeas
pkgver=1.13.0
pkgrel=1
pkgdesc="A configuration editing tool that parses config files and transforms them into a tree"
arch=('x86_64')
url="http://augeas.net"
license=('LGPL')
depends=('libxml2' 'gcc-libs')
validpgpkeys=('AED6E2A185EEB379F17476D2E012D07AD0E3CC30')
source=(https://github.com/hercules-team/augeas/releases/download/release-$pkgver/augeas-$pkgver.tar.gz
        https://github.com/hercules-team/augeas/commit/23f7b550.patch)
sha512sums=('a6a9ee76f3e48780df1928f2fdfa5cc826695fe6233ff90d069d05806cb020aed3c6b2ce6b9047925a203973eefcd7c254fc3ddd1db7473821c8ade4fda58d5f'
            '00f2d5effd613e65a5a8fcce3721d3a7414d2e6097398822f8b3e7a1e28a3f288c441b731525c4658cf27b8cf01bc7bcc917340c649ad7d054f1b6bb605f72eb')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../23f7b550.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
