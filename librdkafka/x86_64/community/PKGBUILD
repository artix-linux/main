# Maintainer: Anatol Pomozov
# Contributor: Andrew Sun <adsun701 at gmail dot com>
# Contributor: Joel Teichroeb <joel at teichroeb dot net>
# Contributor: Alim Gokkaya <alimgokkaya at gmail dot com>

pkgname=librdkafka
pkgver=1.8.2
pkgrel=1
pkgdesc='The Apache Kafka C/C++ library'
arch=(x86_64)
url='https://github.com/edenhill/librdkafka'
license=(BSD)
depends=(libsasl zstd)
makedepends=(python openssl lz4 rapidjson cmake)
source=(librdkafka-$pkgver.tar.gz::https://github.com/edenhill/librdkafka/archive/v$pkgver.tar.gz)
sha256sums=('6a747d293a7a4613bd2897e28e8791476fbe1ae7361f2530a876e0fd483482a6')

build() {
  cmake -S librdkafka-$pkgver -B build -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

check() {
  # TODO: figure out why the tests timeout
  # cmake --build build --target test
  true
}

package() {
  cmake --build build --target install -- DESTDIR="$pkgdir"
}
