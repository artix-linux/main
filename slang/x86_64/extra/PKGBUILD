# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=slang
pkgver=2.3.2
pkgrel=2.1
pkgdesc="S-Lang is a powerful interpreted language"
arch=('x86_64')
url="http://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(http://www.jedsoft.org/releases/slang/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('bbf7f2dcc14e7c7fca40868fd4b411a2bd9e2655')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check ||:
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}
