# Maintainer: Nathan Owens <ndowens@artixlinux.org>

_pipname=nbclassic
pkgname=jupyter-$_pipname
pkgver=0.4.2
pkgrel=1
pkgdesc='Jupyter Notebook as a Jupyter Server Extension'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(jupyter-server jupyter-notebook-shim)
makedepends=(python-build python-installer)
checkdepends=(python-pytest-tornasync)
conflicts=(jupyter-notebook)
provides=(jupyter-notebook)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('b00694d33e844e0f6d02bb6aa4b32e7342d03e27d21b73003ac200978e1bd61a')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  pytest -v
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
