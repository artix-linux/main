# Maintainer: Nathan Owens <ndowens@artixlinux.org>

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=3.4.5
pkgrel=1
pkgdesc='JupyterLab computational environment'
arch=(any)
url='https://github.com/jupyterlab/jupyterlab'
license=(custom)
makedepends=(python-build python-installer python-pre-commit)
depends=(python-jupyterlab_server python-jupyter_packaging jupyter-notebook-shim nodejs)
optdepends=('npm: to install extensions' 'jupyterlab-widgets: ipywidgets support')
checkdepends=('python-pytest-tornasync' 'npm')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz
        no-nbclassic.patch)
sha256sums=('472f6b7996c75f6991592483c26d9fe205a59a71ccbce15842400155dc64f59b'
            'c81b7b957622b7ea68589daa568bba96a8955e768aea0bce60c27fa041e07cc4')

prepare() {
  patch -d $pkgname-$pkgver -p1 < no-nbclassic.patch # Drop nbclassic dependency
  sed -e '/nbclassic/d' -i $pkgname-$pkgver/setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest -v -k 'not test_build'
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
